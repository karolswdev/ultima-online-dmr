//--------------------------------------------------------------------------------

include "include/findcity";
include "include/skills/std";
include "include/buildings";
include "include/dirs";
include "include/calc";

include ":quests:quest_ctrl";
include ":crafting:creation";

include ":harvesting:harvest";
include ":harvesting:iterations";
include ":harvesting:harvcont";

//--------------------------------------------------------------------------------

const REGROW_TIME       := 18000;
const HERD_MAX_AMT      := 50;

const SZANSA_SIEW       := 5;
const SKILL_SIEW_NEEDED := 90;

//--------------------------------------------------------------------------------

program Herding( who, ziolo )

 if(!StandardItemCheck(who,ziolo,NO_MOVABLE_CHECK))
   return;
 endif

  TriggerTipEvent(who, TEID_HARV_HERDING);
 var herd_cfg := ReadConfigFile("herding");
 var type := herd_cfg[ziolo.objtype].type;

 case(type)
  "Krzak":   Harvest(who, ziolo);
  "Ziolo":   Cut(who,ziolo);
  "Siew":    Plant(who,ziolo);
  default:   Sendsysmessage(who,"Nie wiesz co z tym zrobic.",FONT_NORMAL,COLOR_RED);
             return;
 endcase

endprogram

//--------------------------------------------------------------------------------

function Harvest(who,ziolo)

  var IterData := DefineHarvestIterations(who, SKILLID_HERDING);
  var Res;

  while(HarvestIteration(who, IterData))
    Res := DoHarvestHerb(who, ziolo, IterData);
    
    if(Res == error)
      break;
    endif
  endwhile
  
  FinishHarvestIteration(who, IterData);

endfunction

//--------------------------------------------------------------------------------

function DoHarvestHerb(who, ziolo, byref IterData)

  var herd_cfg   := ReadConfigFile("herding");
  var prod       := herd_cfg[ziolo.objtype].Product;
  var skill      := herd_cfg[ziolo.objtype].Skill;
  var points     := herd_cfg[ziolo.objtype].Points;
  var siew       := herd_cfg[ziolo.objtype].Siew;

  if(CInt(GetObjProperty(ziolo,"Herded")))
    if(GetEffectiveSkill(who, SKILLID_HERDING) > 1000)
		var chance := GetEffectiveSkill(who, SKILLID_HERDING) - 1000;
		if(RandomInt(10000) < chance)
			Siew := 1;
		else
			Siew := 0;
		endif
	endif
  endif

  if(!prod or !skill or !points)
    SendSysMessage(who,"Tego nie mozesz zebrac.",FONT_NORMAL,COLOR_RED);
    return error;
  endif

  if(Distance(who,ziolo) > 1)
    SendSysMessage(who,"Stoisz za daleko.",FONT_NORMAL,COLOR_RED);
    return error;
  endif

  var Amt := CInt(1 + CalcSkillExponential(who, SKILLID_HERDING, 10));
  var Amount;

  if(CInt(GetObjProperty(ziolo,"RegrowTime")) < ReadGameClock())
    Amount := HERD_MAX_AMT;
    SetObjProperty(ziolo,"RegrowTime",ReadGameClock() + REGROW_TIME);
  else
    Amount := CInt(GetObjProperty(ziolo,"Amount"));
  endif

  if(!GetObjProperty(ziolo,"RegrowTime"))
    SetObjProperty(ziolo,"RegrowTime",ReadGameClock() + REGROW_TIME);
  endif

  Amt := ToMax(Amt, Amount);
  SetObjProperty(ziolo,"Amount",Amount);
  TurnCharToward(who, ziolo);

  if(!Amount)
    SendSysMessage(who,"Z tej rosliny juz nic wiecej nie zerwiesz.",FONT_NORMAL,COLOR_RED);
    if(CInt(GetObjProperty(ziolo,"Herded")))
      DestroyItem(ziolo);
    endif
    return error;
  else
    if(IterData.curIter == 1)
      SendSysMessage (who, "Zaczynasz zrywac ziola...",FONT_NORMAL,COLOR_GREEN);
    endif

    HarvestIterationGump(who, IterData, "Zrywasz ziola...");
    
    if(!MakeNoise(who, 0, 600))
      return error;
    endif

    var Item;

    if(HarvestOverflowed(who))

      SendSysMessage(who, "Prawie cos zerwal"+ggm(who,3)+", ale czujesz jakby to bylo juz ponad Twoje sily...", FONT_NORMAL, COLOR_ORANGE);

    elseif(CheckSkill(who,SKILLID_HERDING,skill,points))

      SetObjProperty(ziolo,"Amount",Amount - Amt);
      SendSysMessage (who, GetHarvestIterDesc(IterData)+ " Udalo Ci sie zebrac "+Amt+" ziol.",FONT_NORMAL,COLOR_GREEN);

      var itmDesc := GetHarvestDescriptor(prod, who);

      if(IsStackableObj(Prod))
        Item := CreateHarvResource(who, SKILLID_HERDING, itmDesc, Amt);
      else
        var i;
        for(i:=1;i<=Amt;i:=i+1)
          Item := CreateHarvResource(who, SKILLID_HERDING, itmDesc);
          if(!Item)
            break;
          endif
        endfor
      endif

      if(Item)
        SendQuestEvent(who, EVQID_HARVEST, SKILLID_HERDING, Item);
      endif

      if(Siew)
        var harvChance := SZANSA_SIEW + CInt(ToMin(GetEffectiveSkill(who, SKILLID_HERDING) - 95, 0) * 0.9);
        if((GetEffectiveSkill(who, SKILLID_HERDING) >= SKILL_SIEW_NEEDED) and HaveChance(harvChance))
          var itmDesc := GetHarvestDescriptor(siew, who);
          Item := CreateHarvResource(who, SKILLID_HERDING, itmDesc);
          if(Item)
            SendSysMessage(who, "Udalo Ci sie zebrac szczepke!",FONT_NORMAL,COLOR_BLUE);
            SendQuestEvent(who, EVQID_HARVEST, SKILLID_HERDING, Item);
            return 1;
          endif
        endif
      endif
    else
      SetObjProperty(ziolo,"Amount", Amount - CInt(Amt/2));
      SendSysMessage(who, "Nie udalo Ci sie zebrac ziol.",FONT_NORMAL,COLOR_RED);
    endif
  endif

  return 0;

endfunction

//--------------------------------------------------------------------------------

function Cut(who,ziolo)

  var HerbCfg   := ReadConfigFile(":herding:herding");

  if(CInt(HerbCfg[ziolo.objtype].Diff) - 15 > GetEffectiveSkill(who, SKILLID_HERDING))
    SendSysMessage(who, "Twoja wiedza na temat zielarstwa jest zbyt mala by wiedziec jak to pociac.", FONT_NORMAL, COLOR_RED);
    return;
  endif

  var Scissors := FindItemInContainer(who.backpack, UOBJ_SCISSORS);

  if(!Scissors)
    SendSysMessage(who, "Nie masz nozyczek przy sobie.", FONT_NORMAL, COLOR_RED);
    return;
  endif

  var amtToMake := ziolo.amount * (RandomInt(GetCutAmount(who)) + 1);
  
  var harvData     := GetHarvested(ziolo);
  var itmDesc := GetHarvestDescriptorForData(HerbCfg[ziolo.objtype].Product, harvData);
  
  if(DestroyItem(ziolo))
	if(CreateItemInCont(who.backpack, itmDesc, amtToMake, who))
		SendSysMessage(who, "Obcieto ziolo.", FONT_NORMAL, COLOR_BLUE);
	endif
  endif

  //start_script(":herding:cut_herb", array(who, Scissors, ziolo, CRAFT_MAX_ITERATIONS));

endfunction

function GetCutAmount(who)

	return CInt(GetEffectiveSkill(who, SKILLID_HERDING)/20);

endfunction

//--------------------------------------------------------------------------------

function Plant(who,ziolo)

 var herd_cfg := ReadConfigFile("herding");
 var prod     := herd_cfg[ziolo.objtype].Siew;
 var skill    := herd_cfg[ziolo.objtype].Skill;
 var points   := herd_cfg[ziolo.objtype].Points;
 var gs       := GetStand(who.x, who.y, who.realm,who.z);
 
 if(!prod or !skill or !points)
   SendSysMessage(who,"Tego nie mozesz zasadzic.",FONT_NORMAL,COLOR_RED);
   return;
 endif

 SendSysMessage(who, "Gdzie chcial"+ggm(who,1)+" zasadzic ziola?",FONT_NORMAL,COLOR_GREEN);
 var trg := LocTarget( who );

 if(!StandardLocCheck(who,trg))
   return;
 endif

 if(coordist(trg.x,trg.y,who.x,who.y) > 2)
   SendSysMessage (who, "Wybrane miejsce jest za daleko.",FONT_NORMAL,COLOR_RED);
   return;
 endif
 
 if(IsDungeon(who.x,who.y,who.realm) or who.multi.isa(POLCLASS_BOAT) or gs.multi)
   SendSysMessage( who, "Nie mozesz zasadzic ziola w tym miejscu.",FONT_NORMAL,COLOR_RED);
   return;
 endif
 
 if(FindCity(who))
   SendSysMessage( who, "Nie mozesz zasadzic ziola w miescie.",FONT_NORMAL,COLOR_RED);
   return;
 endif

 if(len(Listitemsatlocation(trg.x,trg.y,trg.z,who.realm)) > 0)
   SendSysMessage(who,"Tu nie mozesz zasadzic ziola!",FONT_NORMAL,COLOR_RED);
   return;
 endif

  var parms := {};
  parms[1] := trg.x;
  parms[2] := trg.y;
  parms[3] := trg.z;
  parms[4] := prod;
  parms[5] := who.realm;

 Sendsysmessage(who,"Probujesz zasadzic ziolo...",FONT_NORMAL,COLOR_GREEN);
 
 if(!MakeNoise(who))
   return;
 endif

 if(CheckSkill(who, SKILLID_HERDING, skill, points) and SubtractAmount(ziolo,1))
  SendSysMessage(who, "Udalo Ci sie zasadzic ziolo.",FONT_NORMAL,COLOR_GREEN);
  start_script(":herding:ziolo", parms);
 else
  SendSysMessage(who, "Nie udalo Ci sie zasadzic ziola.",FONT_NORMAL,COLOR_RED);
 endif

endfunction

//--------------------------------------------------------------------------------

 //-----------------------------------------------------------------------------

include "include/checks/objchecks";
include "include/objtypes/skills";
include "include/poison";
include "include/attack";
include ":vampire:vampire";
include "healing";
include ":glyphs:glyphs";
include "include/attributes/npcs_cag";

//-----------------------------------------------------------------------------

const HEAL_MAX_CYCLES := 180;

//-----------------------------------------------------------------------------

var clock := ReadGameClock();
var counter := 0;

//-----------------------------------------------------------------------------

// BloodyStaff//
const GFX_UNSHORN_SHEEP := 0xcf;
const GFX_SHORN_SHEEP   := 0xdf;
const UOBJ_BARREL       := 0x154d;
const UOBJ_TUB          := 0x0e7b;
const UOBJ_TROUGHA      := 0x0b41;
const UOBJ_TROUGHB      := 0x0b42;
const UOBJ_TROUGHC      := 0x0b43;
const UOBJ_TROUGHD      := 0x0b44;

//-----------------------------------------------------------------------------

program UseBandages(who,item)
  
  var patient := SystemFindObjectBySerial(CInt(GetObjProperty(who, "#HealingTarget")));
  EraseObjProperty(who, "#HealingTarget");

  if(!StandardItemCheck(who,item,NO_COMBAT_CHECK))
    return;
  endif

  if(CanMod(who, MOD_BLOCK_SELFHEAL) == 0)
    SendBad(who, "Nie mozesz teraz leczyc sie. Cos Ci przeszkadza!");
    return;
  endif

  var PLvl := GetItemPoisonLvl(item);
  if(PLvl)
    if(item)
      CheckItemPoisonCharges(item);
    endif
    SetPoison(who,PLvl);
    SetObjProperty(who,"#HealBreak",ReadGameClock() + 20);
    SubtractAmount(item, 1);
    return;
  endif

  if(!patient)
    var Bloody := "";
    if(item.objtype == UOBJ_BLOODYBANDAGES)
      Bloody := "zakrwawione ";
    endif

    SendSysmessage( who, "Na czym chcial"+ggm(who,1)+" uzyc " + Bloody + "bandaze ?",FONT_NORMAL,COLOR_GREEN);

    var Targ := LocTarget(who);
    if(item.objtype == UOBJ_BLOODYBANDAGES and !Targ.mobile)
      if(Targ.item)
        Targ := Targ.item;
      endif
      if(Targ.isa(POLCLASS_ITEM) and Targ.objtype == UOBJ_BARREL or Targ.objtype == UOBJ_TUB or Targ.objtype == UOBJ_TROUGHA or Targ.objtype == UOBJ_TROUGHB or Targ.objtype == UOBJ_TROUGHC or Targ.objtype == UOBJ_TROUGHD)
        if(!StandardItemCheck(who,Targ,NO_MOVABLE_CHECK | NEW_DIST_CHECK | NO_OWNER_CHECK, 1))
          return;
        endif
        sleep(3);
        WashBloody(who, item);
      elseif(IsWater(Targ.x, Targ.y, Targ.z, who.realm))
      
        if(!StandardLocCheck(who, Targ, NEW_DIST_CHECK | NO_CHECK_SPECLOS, 3))
          return;
        endif

        if(!MakeWaterSplash(who,Targ))
          return;
        endif
        WashBloody(who, item);
      else
        SendSysmessage( who, "W tym nie umyjesz bandaza!",FONT_NORMAL,COLOR_RED);
      endif
      return;
    endif

    if(Targ.mobile)
      patient := Targ.mobile;
    elseif(Targ.item)
      if(item.objtype == UOBJ_BANDAGE and (Targ.item.objtype in {UOBJ_DK_ALTAR,UOBJ_VAMP_BLOOD} or (Targ.item.objtype >= UOBJ_BLOOD_BIG1 and  Targ.item.objtype <= UOBJ_BLOOD_SMEAR)))
        if(!StandardItemCheck(who,Targ.item,NO_MOVABLE_CHECK | NEW_DIST_CHECK | NO_OWNER_CHECK, 1))
          return;
        endif
        sleep(3);
        MakeBloody(who, item, Targ.item);
        return; 
      endif;
      patient := Targ.item;
    else
      SendSysmessage( who, "Anulowano.",FONT_NORMAL,COLOR_RED);
      return;
    endif
  endif

  if(CInt(GetObjProperty(who,"#HealBreak")) > ReadGameClock())
    SendSysMessage(who,"Nie mozesz teraz tego uzyc...",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(patient.isa(POLCLASS_CORPSE))
    if(!StandardItemCheck(who,patient,NO_MOVABLE_CHECK | NO_COMBAT_CHECK))
      return;
    endif
  else
    if(!StandardMobCheck(who,patient,NOCHECK_DEAD | NO_COMBAT_CHECK))
      return;
    endif
  endif

  /*
  if(IsPlayer(patient) or IsHuman(patient))

    var Kensai := GetBaseSkillBaseValue(who, SKILLID_KENSAI);
    var Barb   := GetBaseSkillBaseValue(who, SKILLID_BARBARIAN);

    if(Kensai > 0)
      if(Kensai >= 600 or HasForgot(who, SKILLID_KENSAI, 86400) or HasBlocked(who, SKILLID_KENSAI, 86400) or IsSetAttrFlag(who, GetAttributeIdBySkillId(SKILLID_KENSAI), ATTRF_BLOCKED))
        SendSysMessage(who, "Nie wiesz co z tym zrobic...",FONT_NORMAL,COLOR_RED);
        return;
      endif
    endif

    if(Barb > 0)
      if(Barb >= 600 or HasForgot(who, SKILLID_BARBARIAN, 86400) or HasBlocked(who, SKILLID_BARBARIAN, 86400) or IsSetAttrFlag(who, GetAttributeIdBySkillId(SKILLID_BARBARIAN), ATTRF_BLOCKED))
        SendSysMessage(who, "Nie wiesz co z tym zrobic...",FONT_NORMAL,COLOR_RED);
        return;
      endif
    endif
    
  endif
  
  */

  if(patient in ListHostiles(who,5))
    SendSysMessage(who,"Nie mozesz go wyleczyc.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(CInt(GetObjProperty(patient,"#HealBreak")) > ReadGameClock())
    SendSysMessage(who,"Nie mozesz teraz leczyc tej osoby...",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(item.objtype == UOBJ_BLOODYBANDAGES and (patient.isa(POLCLASS_NPC) or patient.isa(POLCLASS_CORPSE)))
    SendSysMessage(who,"Do tego potrzebne sa czyste bandaze.",FONT_NORMAL,COLOR_RED);
    return;
  elseif(IsVampire(patient) and item.objtype == UOBJ_BANDAGE)
    SendSysMessage(who,"Czystymi bandazami nie wyleczysz tej osoby.",FONT_NORMAL,COLOR_RED);
    return;
  elseif(!IsVampire(patient) and item.objtype == UOBJ_BLOODYBANDAGES)
    SendSysMessage(who,"Zakrwawionymi bandazami nie wyleczysz tej osoby.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  //Krim tylko jezeli nie leczymy siebie, ani nie leczym swojego zwierzaka
  if((patient != who and GetMaster(patient) != who) and (patient.criminal or patient.murderer))
    SetCriminal(who, 1, "HealingCriminal", "Uleczyl kryminaliste "+GetRealName(patient));
  endif

  NoLongerBusy(who);
  Detach();

  SetObjProperty(who, "#HealTimer", clock);

  if((IsHuman(patient) or IsPlayer(patient)) and !patient.dead)
    var rtype;
    if(item.objtype == UOBJ_BANDAGE)
      rtype := UOBJ_BLOODYBANDAGES;
    else
      rtype := UOBJ_BANDAGE;
    endif;
    if(GetHp(patient) >= GetMaxHp(patient))
      SendSysMessage(who,"Ta osoba jest w pelni zdrowa.",FONT_NORMAL,COLOR_GREEN);
      return;
    endif

    if(Patient == who)
      SendSysMessage(who, "Zaczynasz siebie leczyc...",FONT_NORMAL,COLOR_GREEN);
      if(SubtractAmount(Item , 1))
        heal(who, patient, rtype);
      endif
    else
      if(Patient.dead)
        SendSysMessage(who,"Ta osoba nie zyje.",FONT_NORMAL,COLOR_RED);
        return;
      endif
      if(SubtractAmount(Item , 1) and (Distance(who, patient) <= 1))
        SendSysMessage(who,"Zaczynasz leczyc "+patient.name,FONT_NORMAL,COLOR_GREEN);
        SendSysMessage(patient,who.name+ " zacz"+ggm(who, 2) + " Cie leczyc.",FONT_NORMAL,COLOR_GREEN);
        heal(who, patient, rtype);
      else
        SendSysMessage(who, "Ta osoba jest za daleko.",FONT_NORMAL,COLOR_RED);
        return;
      endif
    endif

  elseif(patient.dead)

    if(!CheckLineOfSight(who,patient))
      SendSysMessage(who,"Nie widzisz juz tej osoby.",FONT_NORMAL,COLOR_RED);
      return;
    endif
    
    if(Distance(who, patient) <= 1)
    
      if(patient.criminal or patient.murderer)
        SetCriminal(who, 1, "ResurrectCriminal", "Wskrzesil kryminaliste "+GetRealName(patient));
      endif
      
      if(SubtractAmount( Item , 1 ))
        SendSysMessage(who,"Probujesz wskrzesic " + patient.name,FONT_NORMAL,COLOR_GREEN);
        SendSysMessage(patient, who.name + " probuje Cie wskrzesic.",FONT_NORMAL,COLOR_GREEN);
        ResurrectPc(who, patient);
      else
        SendSysMessage(who,"Nie masz bandazy.",FONT_NORMAL,COLOR_RED);
        return;
      endif
    else
      SendSysMessage(who, "Ta osoba jest za daleko.",FONT_NORMAL,COLOR_RED);
      return;
    endif

  elseif(patient.isa(POLCLASS_NPC))

    if(!IsTamed(patient))
      SendSysMessage(who,"Ten stwor nie da sie uleczyc!",FONT_NORMAL,COLOR_ORANGE);
      return;
    endif

    if(GetHp(patient) >= GetMaxHp(patient))
      SendSysMessage(who,"To zwierze jest w pelni zdrowe.",FONT_NORMAL,COLOR_GREEN);
      return;
    endif

    if(Distance(who, patient) <= 2)
    
      if((GetMaster(patient) != who) and (patient.criminal or patient.murderer))
        SetCriminal(who, 1, "HealCrimAnimal", "Uleczyl stwora "+GetRealName(patient));
      endif
      
      if(SubtractAmount( Item , 1 ))
        SendSysMessage(who,"Zacz"+ggm(who,5)+" leczyc "+patient.name,FONT_NORMAL,COLOR_GREEN);
        HealAnimal(who, patient);
      else
        SendSysMessage(who,"Nie masz bandazy.",FONT_NORMAL,COLOR_RED);
        return;
      endif
    else
      SendSysMessage(who, "Zwierze jest za daleko.",FONT_NORMAL,COLOR_RED);
      return;
    endif

  elseif(patient.isa(POLCLASS_CORPSE))

    if(GetBaseSkill(who,SKILLID_VETERINARY) >= 81 and GetBaseSkill(who, SKILLID_ANIMALLORE) >= 81)
      if(Distance(who,patient) > 3)
        SendSysMessage(who,"Stoisz za daleko.",FONT_NORMAL,COLOR_RED);
        return;
      else
        ResurrectAnimal(who, Item, patient);
      endif
    else
      SendSysMessage( who , "Twoje umiejetnosci sa zbyt male by probowac wskrzesic zwierze.",FONT_NORMAL,COLOR_RED);
      return;
    endif

  else

    SendSysMessage( who , "Nie mozesz tego wyleczyc.",FONT_NORMAL,COLOR_RED);
    return;

  endif

  EraseObjProperty(who,"#HealTimer");

endprogram

//-----------------------------------------------------------------------------

function Heal(who, patient, recivedobjtype)

  var Healing      := GetEffectiveSkill(who, SKILLID_HEALING);
  var Anatomy      := GetEffectiveSkill(who, SKILLID_ANATOMY);

  var chp          := GetHP(who);
  var healamt      := CInt((CInt(Healing / 20) + CInt(Anatomy / 20) + 5) * (((Anatomy + (Healing * 2)) + 100) * 0.01));
  var cycles       := GetHealCycles(who,patient);
  var Sx           := who.x;
  var Sy           := who.y;
  var MaxSteps     := CInt(GetEffectiveSkill(who,SKILLID_HEALING) / 4) + 15;
  var bLastDist    := 0;
  var LastDist     := 0;
  var BaseCycles   := cycles;

  if(CInt(GetObjProperty(patient,"#SlowHeal")) > ReadGameClock())
    bLastDist := 1;
  endif
  
  var waitCycle := 100;
  
  // goqsane: 02-24-2018: Added new reagent... grzyb_magic: should put down the delay per each consumed magic mushroom
	var specialReagent := FindItemInContainer(who.backpack, "grzyb_magic");
  
	if(specialReagent)
		if(SubtractAmount(specialReagent, 1))
			waitCycle := 82;
		endif
	else
		waitCycle := 100;
	endif
	
	var mod := GetGlyphMod(who, "WimprovedHealing");
	if(mod)
		waitCycle := waitCycle - mod;
		if(RandomInt(10) == 1)
			SendSysMessage(who, "Glif szybkiego leczenia skraca czas.", FONT_NORMAL, COLOR_GREEN);
		endif
	endif

  while(counter < cycles)
	
	sleepms(waitCycle);

    if(bLastDist)
      if(LastDist < coordist(Sx,Sy,who.x,who.y))
        LastDist := coordist(Sx,Sy,who.x,who.y);
        cycles   := ToMax(BaseCycles + CInt(LastDist / 0.6), HEAL_MAX_CYCLES);
      endif
    endif

    if(Distance(who, patient) > 1)
      SendSysMessage(who,"Ta osoba jest za daleko.",FONT_NORMAL,COLOR_RED);
      SendSysMessage(patient, who.name + " przestal" + ggm(who, 4) + " Cie leczyc.",FONT_NORMAL,COLOR_RED);
      break;
    endif

    if(!IsHealing(who,patient,"leczyc"))
      break;
    endif

    if((GetHP(who) < chp))
      if(RandomInt(chp - GetHp(who)) > CInt(GetBaseDexterity(who) / 1.25))
        SendSysMessage( who, "Twoj palec sie posliznal.",FONT_NORMAL,COLOR_RED);
        healamt := CInt(GetHealAmt(healamt,cycles,counter) * 0.30);
        counter := cycles;
      endif
    elseif(coordist(who.x,who.y,Sx,Sy) > MaxSteps)
      SendSysMessage( who, "Twoj palec sie posliznal.",FONT_NORMAL,COLOR_RED);
      healamt := CInt(GetHealAmt(healamt,cycles,counter) * 0.50);
      counter := cycles;
    endif
    if(who.paralyzed or who.frozen)
      if(RandomInt(100) > CInt(GetBaseDexterity(who) / 3))
        SendSysMessage( who, "Twoj palec sie posliznal.",FONT_NORMAL,COLOR_RED);
        healamt := CInt(GetHealAmt(healamt,cycles,counter) * 0.75);
        counter := cycles;
      endif
    endif

    if(patient.dead)
      SendSysMessage( who, "Ta osoba umarla!",FONT_NORMAL,COLOR_RED);
      break;
    endif

    counter := counter + 1;

    if(counter >= cycles or CInt(GetObjProperty(who,"#HealBreak")) > ReadGameClock())
      var diff := 90 - Cint((GetHP(patient) * 100.0) / GetMaxHP(patient));
      if(diff < 0)
        diff := 0;
      endif
      if(CheckSkill(who, SKILLID_HEALING, diff, 0) and healamt > 0)
        if(healamt <= 10)
          SendSysMessage(who, "Skonczyl"+ggm(who, 3) + " leczenie, ale ono ledwo co pomoglo.",FONT_NORMAL,COLOR_GREEN);
        endif
		
		var mod := GetGlyphMod(who, "WincreasedHealing");
		if(mod)
			healamt := healamt + mod;
			if(RandomInt(10) == 1)
				SendSysMessage(who, "Sila glifu zwieksza ilosc uleczonych obrazen.", FONT_NORMAL, COLOR_GREEN);
			endif
		endif
		
        if((GetMaxHP(patient) - GetHP(patient)) >= healamt)
          HealMobile(patient, healamt, who);
        else
          healamt := (GetMaxHP(patient) - GetHP(patient));
          HealMobile(patient, healamt, who);
        endif
        
        var hpoints  := ToMax(GetDefaultPointsValue(SKILLID_HEALING), healamt*2);

        if(patient == who and GetHP(patient) >= GetMaxHP(patient))
          if(CanGainFromEnemies(who))
            AwardPoints(who, SKILLID_HEALING,hpoints);
            AwardPoints(who, SKILLID_ANATOMY,CInt(hpoints * SECOND_SKILL_GAIN_MOD), NO_STAT_AWARD);
          endif
          SendSysMessage(who, "Wyleczyl"+ggm(who, 3) + " sie kompletnie.",FONT_NORMAL,COLOR_BLUE);
        elseif(patient == who)
          if(CanGainFromEnemies(who))
            AwardPoints(who, SKILLID_HEALING,hpoints);
            AwardPoints(who, SKILLID_ANATOMY,CInt(hpoints * SECOND_SKILL_GAIN_MOD), NO_STAT_AWARD);
          endif
          SendSysMessage(who, "Wyleczyl"+ggm(who, 3) + " sie.",FONT_NORMAL,COLOR_GREEN);
        elseif(GetHP(patient) >= GetMaxHP(patient))
        
          if(CanGainFromEnemies(patient))
            AwardPoints(who, SKILLID_HEALING,hpoints);
            AwardPoints(who, SKILLID_ANATOMY,CInt(hpoints * SECOND_SKILL_GAIN_MOD), NO_STAT_AWARD);
          endif

          SendSysMessage(who, "Wyleczyl"+ggm(who, 3) + " ta osobe kompletnie.",FONT_NORMAL,COLOR_BLUE);
          SendSysMessage(patient, who.name + " wyleczyl"+ ggm(who, 4) + " Cie kompletnie.",FONT_NORMAL,COLOR_BLUE);
        else
        
          if(CanGainFromEnemies(patient))
            AwardPoints(who, SKILLID_HEALING,hpoints);
            AwardPoints(who, SKILLID_ANATOMY,CInt(hpoints * SECOND_SKILL_GAIN_MOD), NO_STAT_AWARD);
          endif
          SendSysMessage(who,"Wyleczyl"+ggm(who, 3) + " osobe " + patient.name,FONT_NORMAL,COLOR_GREEN);
          SendSysMessage(patient, who.name + " Cie wyleczyl"+ ggm(who, 4) + ".",FONT_NORMAL,COLOR_GREEN);
        endif
        if(patient != who)
          SendHealToHostile(patient, who, healamt);
        endif
      else
        SendSysMessage ( who , "Nie udalo Ci sie wyleczyc tej osoby.",FONT_NORMAL,COLOR_RED);
      endif
      break;
    endif
  endwhile

  CreateiteminPack(who, recivedobjtype, 1);

endfunction

//-----------------------------------------------------------------------------

function HealAnimal(who,patient)

  if(patient.isa(POLCLASS_NPC))

    var cfgElem := GetNpcConfig(patient);

    if(cfgElem.NoHeal)
      SendSysMessage(who,"Tego tak nie wyleczysz.",FONT_NORMAL,COLOR_RED);
      return;
    endif
  endif

  var Healing := GetEffectiveSkill(who,SKILLID_VETERINARY);
  var Anatomy := GetEffectiveSkill(who,SKILLID_ANIMALLORE);

  var chp;
  var healamt := CInt(Healing / 2) + CInt(Anatomy / 2) + 60;
  var manaamt := CInt(Healing / 25) + CInt(Anatomy / 30);
  var cycles  := GetHealCycles(who,patient);

  while(counter <= cycles)

    if(!IsHealing(who,patient,"leczyc","stworzenie"))
      break;
    endif

    chp := GetHp(who);
    sleepms(100);

    if(Distance(who, patient) > 3)
      SendSysMessage(who,"Zwierze jest za daleko.",FONT_NORMAL,COLOR_RED);
      break;
    endif

    if(GetHp(who) < chp)
      if((RandomInt(60)+1) > GetDexterity(who))
        SendSysMessage( who, "Palec Ci sie posliznal.",FONT_NORMAL,COLOR_RED);
        healamt := CInt(GetHealAmt(healamt,cycles,counter) * 0.40);
      endif
    endif
    if(who.paralyzed or who.frozen)
      SendSysMessage( who, "Twoj palec sie posliznal.",FONT_NORMAL,COLOR_RED);
      healamt := CInt(GetHealAmt(healamt,cycles,counter) * 0.75);
      counter := cycles;
    endif

    if(who.dead)
      SendSysMessage(who,"Przerwal" + ggm(who,3) + " leczenie.",FONT_NORMAL,COLOR_RED);
      break;
    endif

    if(counter >= cycles or CInt(GetObjProperty(who,"#HealBreak")) > ReadGameClock())
      if(CheckSkill(who, SKILLID_VETERINARY, -1, 0))
        if(healamt <= 20)
          SendSysMessage(who,"Skonczyl"+ggm(who,3)+" bandazowanie, ale ono ledwo pomoglo.",FONT_NORMAL,COLOR_GREEN);
        endif
        HealMobile(patient, healamt, who);
        HealMana(patient, manaamt, who);

//        if(patient.warmode or patient.opponent)
        if(!IsAnyCagTamedEmpty(patient)) // sprawdzamy tutaj, czy gosc nie sparuje biernie miedzy dwoma tamed mobami 
          AwardPoints(who, SKILLID_VETERINARY, CInt(healamt * 2));
          AwardPoints(who, SKILLID_ANIMALLORE,CInt(healamt / 2 * SECOND_SKILL_GAIN_MOD), NO_STAT_AWARD);
        endif
//        endif

        if(GetHp(patient) == GetMaxHp(patient))
          SendSysMessage(who, "Wyleczyl"+ggm(who,3)+" zwierze kompletnie.",FONT_NORMAL,COLOR_BLUE);
        else
          SendSysMessage(who, "Wyleczyl"+ggm(who,3)+" zwierze " + patient.name,FONT_NORMAL,COLOR_GREEN);
        endif
      else
        if(Healing < 30)
          AwardPoints(who, SKILLID_VETERINARY, CInt(healamt / 4));
        endif
        SendSysMessage( who, "Nie udalo Ci sie go uzdrowic.",FONT_NORMAL,COLOR_RED);
      endif
      break;
    endif
    counter := counter + 1;

  endwhile

  CreateItemInPack(who, UOBJ_BLOODYBANDAGES, 1);

endfunction

//-----------------------------------------------------------------------------

function GetHealAmt(healamt,cycles,counter)

  return CInt((healamt * (((counter * 100) / cycles) * 0.01)));

endfunction

//-----------------------------------------------------------------------------

function ResurrectAnimal(who, Item, patient)

  var Temp    := GetObjProperty(patient, PROP_NPCTEMP);
  var cfgElem := GetNpcConfig(Temp);
  var Diff    := cfgElem.tameskill;

  if((cfgElem.NoHeal or !Temp or Diff == error ) and !IsAdmin(who))
    SendSysMessage(who,"Tego nie uda Ci sie wskrzesic.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  Diff := CInt(Diff);

  if(Diff - 5 >= 0)
    Diff := Diff - 5;
  endif

  var Healing := GetEffectiveSkill(who, SKILLID_VETERINARY);

  if(who == patient)
    return;
  endif

  var Name := patient.name;

  Name["Zwloki "] := "";

  if(!SubtractAmount(Item,1))
    SendSysMessage(who,"Nie masz bandazy.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(!CanResurrectNpc(who, patient))
    return;
  endif

  SendSysMessage(who,"Probujesz wskrzesic zwierze " + Name,FONT_NORMAL,COLOR_GREEN);

  while(counter <= 10 and patient and IsLoggedIn(who))
    PlayPcStEffect(patient.x, patient.y, patient.z, patient.realm, FX_HEAL_EFFECT, 90, 0x1f, 0x1f, 4);
    PlaySoundEffect(who, SFX_SPELL_CUNNING);
    sleep(1);

    if(Distance(who, patient) > 3)
      SendSysMessage(who,"Stoisz za daleko.",FONT_NORMAL,COLOR_RED);
      break;
    endif

    if(!IsHealing(who,patient,"wskrzeszac", "zwierze") or !patient)
      break;
    endif
    
    if(who.dead or who.frozen)
      SendSysMessage(who,"Przerwal" + ggm(who,3) + " wskrzeszanie.",FONT_NORMAL,COLOR_RED);
      break;
    endif

    if(counter >= 10)
      if(CheckSkill(who, SKILLID_VETERINARY, Diff, 0))
        if(ResurrectNpc(who, patient, (1.0 - (Healing * 0.5))))
        
          if(!IsAnyCagTamedEmpty(patient))
            AwardPoints(who, SKILLID_VETERINARY, 100);
            AwardPoints(who, SKILLID_ANIMALLORE,CInt(100 * SECOND_SKILL_GAIN_MOD), NO_STAT_AWARD);
          endif

          SendSysMessage(who, "Wskrzesil"+ggm(who, 3) + " zwierze " + Name+".",FONT_NORMAL,COLOR_BLUE);
          break;
        endif
      else
        SendSysMessage(who, "Nie udalo Ci sie wskrzesic tego zwierzecia.",FONT_NORMAL,COLOR_RED);
      endif
    endif
    counter := counter + 1;
  endwhile

endfunction

//-----------------------------------------------------------------------------

function WashBloody(who, Bloody)
  
  var Amount := GetEffectiveSkill(who,SKILLID_HEALING);

  if(GetEffectiveSkill(who, SKILLID_VETERINARY) > Amount)
    Amount := GetEffectiveSkill(who, SKILLID_VETERINARY);
  endif

  Amount := CInt(Amount); 

  if(!Amount)
    Amount := 1;
  endif

  if(Amount > Bloody.amount)
    Amount := Bloody.amount;
  endif

  if(SubtractAmount(Bloody, Amount))
    CreateItemInPack(who, UOBJ_BANDAGE, Amount);
  else
    return;
  endif

  var Msg := "";

  case(Amount)
    1:       msg := "Umyl"+ggm(who,3)+" bandaz.";
    2:
    3:
    4:       msg := "Umyl"+ggm(who,3)+" "+Amount+" bandaze.";
    default: msg := "Umyl"+ggm(who,3)+" "+Amount+" bandazy.";
  endcase

  SendSysmessage(who,msg,FONT_NORMAL,COLOR_BLUE);
  
endfunction


function MakeBloody(who, Bandage, item)
  var Amount;
  if(item.objtype == UOBJ_DK_ALTAR or (item.objtype >= UOBJ_BLOOD_BIG1 and  item.objtype <= UOBJ_BLOOD_SMEAR))
    Amount := GetEffectiveSkill(who,SKILLID_HEALING);
    Amount := CInt(Amount / 5);
    if(!Amount)
      Amount := 1;
    endif

    if(Amount > Bandage.amount)
      Amount := Bandage.amount;
    endif

    if(!SubtractAmount(Bandage, Amount))
      return 0;
    endif
  elseif(item.objtype == UOBJ_VAMP_BLOOD)
    Amount := ToMax(Bandage.amount ,item.amount * 10);
    if(SubtractAmount(Bandage, Amount))
      if(!SubtractAmount(item, CInt((Amount-1)/10)+1))
        CreateItemInPack(who, UOBJ_BANDAGE, Amount);
        return 0;
      else
        CreateItemInPack(who, UOBJ_VIAL, CInt((Amount-1)/10)+1);
      endif
    endif
  else
    return 0;
  endif
  
  CreateItemInPack(who, UOBJ_BLOODYBANDAGES, Amount);


  var Msg := "";

  case(Amount)
    1:       msg := "Zakrwawil"+ggm(who,3)+" bandaz.";
    2:
    3:
    4:       msg := "Zakrwawil"+ggm(who,3)+" "+Amount+" bandaze.";
    default: msg := "Zakrwawil"+ggm(who,3)+" "+Amount+" bandazy.";
  endcase

  SendSysmessage(who,msg,FONT_NORMAL,COLOR_BLUE);
  return 1;
endfunction

//-----------------------------------------------------------------------------

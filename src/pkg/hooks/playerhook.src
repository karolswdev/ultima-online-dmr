//-----------------------------------------------------------------------------

include "include/clock";
include "include/resurrect";
include "include/findcity";
include "include/tempmods";
include "include/getobj";
include "include/log";
include "include/reputation/penalty";
include "include/reputation/murderer";
include "include/faith";
include "include/player/nutrition";
include "include/mobile/age";
include "include/player/target";
include "include/player/misc";
include "include/player/young";
include "include/client/misc";
include "include/attack";

include ":weather:weather";
include ":wearwolf:wearwolf";
include ":vampire:vampire";
include ":makrocheck:makro";
include ":guildstone:guild";
include ":housing:housing";
include ":jail:common";
include ":miracleworker:ritual";
include ":character:race_validation";
include ":happyhour:common";

//-----------------------------------------------------------------------------

const HNF_WATER              := 0x01;
const HNF_SWAMP              := 0x02;
const HNF_JUNGLE             := 0x04;
const HNF_FOREST             := 0x08;
const HNF_GRASS              := 0x10;
const HNF_DIRT               := 0x20;

//-----------------------------------------------------------------------------

var MakroCheck  := ReadGameClock() + MAKROCHECK_TIME + RandomInt(CInt(MAKROCHECK_TIME / 2));
var cfgRaces    := ReadConfigFile(RACES_CFG);
var hnFlags     := 0;
var Coords;

//-----------------------------------------------------------------------------

program PlayerHook(who)

  SetLogRunCycles(LST_HOOK);

  SetObjProperty(who, PROP_PLAYERHOOK_PID,GetPid());

  CheckJail(who);
  CheckMakro(who);
  CheckMurderLongCount(who);

  /*__NOPVP__  
  if(GetPvPStatus(who) == error)
    if(IsYoung(who))
      SetPvPStatus(who,PVP_NO);
    else
      SetPvPStatus(who,PVP_YES);
    endif
  endif*/

  
  if(GetObjProperty(who,"blockfallmurderer") < ReadGameClock())
    MurdererFall(who);
  endif
  
  sleep(2);

  Coords := {who.x,who.y,who.realm};
  UpdateNatureFlags(who);

  var LastTrg, LastSk;

  while(who and IsLoggedIn(who))

    if(GetObjProperty(who, PROP_PLAYERHOOK_PID) != GetPid())
      return;
    endif

    CheckYoung(who);
    CheckTimers(who);
    CheckDelayedMaxSkill(who);
    
    if(GetObjProperty(who, PROP_REGENERATIONEND) and GetObjProperty(who, PROP_REGENERATIONEND) < ReadGameClock())
      FinishRitual(who);
    endif

    if(!who.dead)
      CheckWeight(who);
      CheckHunger(who);
      CheckWearWolf(who);
      CheckVampire(who);
      CheckTitle(who);
      /*
        __NONPVP__ DELETED
      CheckPvPStatus(who);*/
      //SendSeasonPacket(who);
    endif

    sleepms(500);

    CheckHairs(who);
    CheckMurdPenalty(who);

    sleepms(500);

    if(!who.cmdlevel and !IsOnDMRTest())
      if(!who.multi.isa(POLCLASS_BOAT) and (coordist(Coords[1], Coords[2], who.x, who.y) > 10 or who.realm != Coords[3]))
        MakroCheck := ReadGameClock() + MAKROCHECK_TIME + RandomInt(CInt(MAKROCHECK_TIME / 2));
        Coords := {who.x,who.y,who.realm};
      endif

      if(MakroCheck < ReadGameClock())
        LastTrg := GetObjProperty(who, PROP_LTRG_USE);
        LastSk  := GetObjProperty(who, PROP_LSK_USE);

        if((PolCore().systime - LastTrg[2]) < CInt(MAKROCHECK_TIME/2) or (PolCore().systime - LastSk[2]) < CInt(MAKROCHECK_TIME/2))
          SendMakrorer(who);
          MakroCheck := ReadGameClock() + MAKROCHECK_TIME + RandomInt(CInt(MAKROCHECK_TIME / 2));
        endif
      endif
    endif

    if(!who.cmdlevel)
      sleep(3);
      UpdateNatureFlags(who);
      PlayHookWaterSound(who);
      sleep(3);
    else
      sleep(10);
    endif

    CheckTimers(who);

    if(!who.cmdlevel)

      PlayHookNatureSounds(who);
      PlayHookWaterSound(who);

      sleep(3);
      PlayHookWaterSound(who);

      PlayHookNatureSounds(who);
    else
      sleep(10);
    endif

    CheckHappyHourTimers(who);

  endwhile

endprogram

//-----------------------------------------------------------------------------

function UpdateNatureFlags(who)

  hnFlags := 0;

  if(IsWaterNearby(who.x, who.y, who.realm))
    SetBitOn(hnFlags, HNF_WATER);
  endif

  var MapInfo := GetMapInfo(who.x, who.y, who.realm);

  if(IsSwampTile(MapInfo.landtile))
    SetBitOn(hnFlags, HNF_SWAMP);
  elseif(IsTile(MapInfo.landtile, LTILE_JUNGLE))
    SetBitOn(hnFlags, HNF_JUNGLE);
  elseif(IsTile(MapInfo.landtile, LTILE_FOREST))
    SetBitOn(hnFlags, HNF_FOREST);
  elseif(IsTile(MapInfo.landtile, LTILE_GRASS))
    SetBitOn(hnFlags, HNF_GRASS);
  elseif(IsTile(MapInfo.landtile, LTILE_DIRT))
    SetBitOn(hnFlags, HNF_DIRT);
  endif

endfunction

//-----------------------------------------------------------------------------

function PlayHookWaterSound(who)

  if(hnFlags & HNF_WATER)
    PlayWaterSound(who);
  endif

endfunction

//-----------------------------------------------------------------------------

function PlayHookNatureSounds(who)

  if(RandomInt(100) < 75)
    if(!IsRaining())
      if(hnFlags & HNF_SWAMP)
        PlaySwampSound(who);
      elseif(hnFlags & HNF_JUNGLE)
        PlayJungleSound(who);
      elseif(GetCurrentSeason() != SEASON_WINTER)
        if(hnFlags & HNF_FOREST)
          PlayForestSound(who);
        elseif(hnFlags & HNF_GRASS)
          PlayNightSound(who);
        elseif(hnFlags & HNF_DIRT)
          PlayNightSound(who);
        endif
      elseif(RandomInt(100) < 30)
        if(hnFlags & HNF_GRASS)
          PlayNightSound(who);
        elseif(hnFlags & HNF_DIRT)
          PlayNightSound(who);
        endif
      endif
    endif
  endif

endfunction

//--------------------------------------------------------------------------------

function CheckDonationMessage(who)

  if(!GetObjProperty(who, "DonationMessage"))
    return;
  endif

  var gc := ReadGameClock();

  var time := CInt(GetObjProperty(who, "DonationMessageTime"));

  if(time)
    var difference := gc - time;
    if(difference > 2592000)
      EraseObjProperty(who, "DonationMessage");
      EraseObjProperty(who, "DonationMessageTime");
      SendSysMessage(who, "Stracil" + GGm(who, 3) + "swoj tytul...", FONT_NORMAL, COLOR_RED);
    endif
  endif

endfunction

function CheckTitle(who)

  if(!GetObjProperty(who, PROP_CUSTOM_TITLE))
    return;
  endif

  var expiry := CInt(GetObjProperty(who, PROP_CUSTOM_TITLE_TIME));

  if(ReadGameClock() > expiry)
    SendSysMessage(who, "Stracil" + GGm(who, 3) + " swoj tytul szlachecki.");
    EraseObjProperty(who, PROP_CUSTOM_TITLE);
    EraseObjProperty(who, PROP_CUSTOM_TITLE_TIME);
    return;
  endif

endfunction

function CheckHunger(who)

  if(who.cmdlevel)
    return;
  endif

  if(GetAge(who) < 23)
      SetNutrValue(who, NVI_VITAMIN, 75);
      SetNutrValue(who, NVI_PROTEIN, 75);
      SetNutrValue(who, NVI_HUNGER,  75);
      SetNutrValue(who, NVI_SUGAR,   75);
    return;
  endif

  var wellFed := CInt(GetObjProperty(who, "WellFed"));

  if(wellFed)
    if(wellFed > ReadGameClock())
	return;
    else
	EraseObjProperty(who, "WellFed");
    endif
  endif

  if(IsJailed(who) or who.dead)
    return;
  endif

  var hungTime := GetObjProperty(who, PROP_LASTHUNGER_TIME);
  var hDelay   := GetObjProperty(who, PROP_HUNGERDELAY);

  if(CInt(hungTime) <= 0)
    hungTime := GetObjProperty(who, PROP_ONLINETIMER);
    SetObjProperty(who, PROP_LASTHUNGER_TIME, hungTime);
  endif

  if(CInt(hDelay) <= 0)
    hDelay := HGDELAY_MIN_SEC;
    SetObjProperty(who, PROP_HUNGERDELAY, hDelay);
  endif

  if(hungTime + hDelay < GetObjProperty(who, PROP_ONLINETIMER))
    if(IsVampire(who) or IsWearWolf(who))
      SetNutrValue(who, NVI_HUNGER,  GetNutrValue(who, NVI_HUNGER)  - HUNGER_DEC_VAL);
      SetNutrValue(who, NVI_PROTEIN, GetNutrValue(who, NVI_PROTEIN) - HUNGER_DEC_VAL);
      SetNutrValue(who, NVI_VITAMIN, GetNutrValue(who, NVI_VITAMIN) - HUNGER_DEC_VAL);
      SetNutrValue(who, NVI_SUGAR,   GetNutrValue(who, NVI_SUGAR)   - HUNGER_DEC_VAL);
    else
      SetNutrValue(who, NVI_HUNGER,  GetNutrValue(who, NVI_HUNGER)  - HUNGER_DEC_VAL - RandomInt(2));
      SetNutrValue(who, NVI_PROTEIN, GetNutrValue(who, NVI_PROTEIN) - HUNGER_DEC_VAL - RandomInt(2));
      SetNutrValue(who, NVI_VITAMIN, GetNutrValue(who, NVI_VITAMIN) - HUNGER_DEC_VAL - RandomInt(2));
      SetNutrValue(who, NVI_SUGAR,   GetNutrValue(who, NVI_SUGAR)   - HUNGER_DEC_VAL - RandomInt(2));
    endif

    SetObjProperty(who, PROP_HUNGERDELAY, HGDELAY_MIN_SEC);
    SetObjProperty(who, PROP_LASTHUNGER_TIME, GetObjProperty(who, PROP_ONLINETIMER));
  else
    if(GetNutrValue(who, NVI_HUNGER) < CRITICAL_HUNGER_VAL)
      hungTime := GetObjProperty(who, PROP_ONLINETIMER) - hungTime;
      if((hungTime % (CInt(hDelay/10))) > CInt(hDelay/10.5))
        SendNutrMsg(who, GetNutrValue(who, NVI_HUNGER));
      endif
    endif
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckWeight(who)

  if(!who.cmdlevel and !who.dead)
    var maxWeight := GetFullWeight(who);
    if(who.weight > maxWeight)
      DamageByWeight(who,(5*GetStrength(who))/10);
      if(who.weight > (maxWeight * 1.5))
        DamageByWeight(who,(2*GetStrength(who))/10);
      endif
      if(who.weight > (maxWeight * 2))
        DamageByWeight(who,(3*GetStrength(who))/10);
      endif
      SendSysMessage(who, "Slyszysz chrupniecie swych kosci.",FONT_NORMAL,COLOR_RED);
      PlaySoundEffect(who,0x01C4);
    endif
  endif

endfunction

//-----------------------------------------------------------------------------

function DamageByWeight(who,Dmg)

  DealDamage(who,CInt(Dmg),who);
  SetObjProperty(who,"#HealBreak",ReadGameClock() + 30);

endfunction

//-----------------------------------------------------------------------------

function DamageByHunger(who,Dmg)

  DealDamage(who,CInt(Dmg),who);
  SetObjProperty(who,"#HealBreak",ReadGameClock() + 30);

endfunction

//-----------------------------------------------------------------------------

function CheckTimers(who)

  CheckMurderCountDecay(who);

  if(!CInt(GetObjProperty(who,PROP_LOGONTIME)))
    SetObjProperty(who,PROP_LOGONTIME,ReadGameClock());
  endif

  var logonTime := ReadGameClock() - CInt(GetObjProperty(who,PROP_LOGONTIME));
  SetObjProperty(who,PROP_LOGONTIME, ReadGameClock());
  if(!IsGainBlockTimed(who))
    SetObjProperty(who,PROP_ONLINETIMER,CInt(GetObjProperty(who,PROP_ONLINETIMER)) + logonTime);
  else
    SendSysMessage(who, "Twoj czas online nie liczy sie z powodu blokady.", FONT_NORMAL, COLOR_RED);
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckMurderCountDecay(who)

  var murdTime := CInt(GetObjProperty(who,"longmurders"));

  if(murdTime >= MAX_MURD_TIMES)
    CheckLongTermCounts(who);
  endif

  murdTime := CInt(GetObjProperty(who,"shortmurders"));
  if(murdTime > 0)
    CheckShortTermCounts(who);
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckLongTermCounts(who)

  var membStatus := GetMemberStatus(who);

  if(membStatus and membStatus == CHAOS_ST)
    membStatus := ReadGameClock() - GetObjProperty(who,PROP_LOGONTIME);
    SetObjProperty(who,"decaylongcountat", GetObjProperty(who,"decaylongcountat") + membStatus);
  elseif(CInt(GetObjProperty(who,PROP_ONLINETIMER)) > CInt(GetObjProperty(who,"decaylongcountat")))
    if(CanMod(who,MOD_STATUS) != 0)
      EraseMurderer(who);
    endif
    return;
  endif

  if(!who.murderer)
    if(CanMod(who,MOD_STATUS) != 0)
      SetMurderer(who);
    endif
  endif

  CheckPaladinMurderer(who);

endfunction

function CheckHappyHourTimers(who)

  var available := GetAvailableHHTime(who);

  if(!available)
    if(IsHHStarted(who))
      StopHH(who);
      SendSysMessage(who, "Czas zwiekszonej nauki skonczyl sie.", FONT_NORMAL, COLOR_BLUE);
    endif
  endif
  
  if(IsHHStarted(who) && available > 0)
    if(available <= 10)
      SendSysMessage(who, "Czas zwiekszonej nauki dobiega konca.", FONT_NORMAL, COLOR_RED);
      StopHH(who);
    else
      SetAvailableHHTime(who, available - 10);
    endif
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckShortTermCounts(who);

  if(CanMod(who,MOD_STATUS) != 0)
    if(CInt(GetObjProperty(who,PROP_ONLINETIMER)) and CInt(GetObjProperty(who,PROP_ONLINETIMER)) > CInt(GetObjProperty(who,"decayshortcountat")))
      SetObjProperty(who,"shortmurders",0);
      SetCriminal(who, 0);
    elseif(!who.criminal)
      var time := CInt(GetObjProperty(who,PROP_ONLINETIMER)) - ( CInt(GetObjProperty(who,"decayshortcountat")) - SHORT_COUNT_DECAY_TIME );
      SetCriminal(who, 1, "CheckShortTermCounts", "Refresh krima za zabicie osobnika, ktory zglosil morderstwo "+RealTime(time)+" temu.");
    endif
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckWearWolf(who)
  var currHour := GetHour();
  var currDay := GetDay();

  if(GetObjProperty(who, "WearwolfStopped"))
    if(currHour >= WW_END_HOUR and currHour < WW_START_HOUR and (GetObjProperty(who, "WearwolfStopped") != currDay))
      EraseObjProperty(who, "WearwolfStopped");
    else
      EndMorph(who);
    endif
  endif

  if(IsWearWolf(who) and !GetObjProperty(who, "WearwolfStopped"))
  
    if(GetObjProperty(who, "WWDebug") and IsOnDMRTest())
      StartMorph(who);
    elseif(currHour == ConvertToTime(WW_START_HOUR - 1))
      InitiateMorph(who);
    elseif(currHour >= WW_START_HOUR or currHour < WW_END_HOUR)
      StartMorph(who);
    elseif(currHour == WW_END_HOUR)
      FinishMorph(who);
    else
      EndMorph(who);
    endif
    
  elseif(CanBeWearWolf(who))
    CheckWearWolfAcceptation(who);
  endif
  

  if(IsWearWolf(who) and !GetObjProperty(who, "WearwolfStopped"))
    if(currHour == ConvertToTime(WW_END_HOUR - 1))
      SendSysMessage(who, "Odczuwasz powolny spadek sil.", FONT_NORMAL, COLOR_RED);
    elseif(currHour == ConvertToTime(WW_START_HOUR - 2))
      SendSysMessage(who, "Powoli tracisz kontrole nad swoim cialem.", FONT_NORMAL, COLOR_BLUE);
    endif
  endif

 endfunction

//----------------------------------------------------------------------------- 

function CheckVampire(who)

  if(IsVampire(who))
    var Hour := GetHour();

    if(GetObjProperty(who, "VWDebug") and IsOnDMRTest())
      VampireChange(who);
    elseif((Hour >= VAMPIRE_HOUR and Hour <= 24) or (Hour >= 0 and Hour < VAMPIRE_END_HOUR))
      VampireChange(who);
    else
      VampireEnd(who);
    endif
  elseif(CanBeVampire(who))
    CheckVampireAcceptation(who);
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckJail(who)

  if(IsJailed(who) and !who.frozen)
    who.frozen := 1;
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckMakro(who)

  if(GetObjProperty(who,"Oszust"))
    if(GetObjProperty(who,"Oszust") + OSZUST_TIME < GetObjProperty(who,PROP_ONLINETIMER))
      var Name := who.name;
      Name[" - Oszustka"] := "";
      Name[" - Oszust"]   := "";
      SetName(who,Name);
      EraseObjProperty(who,"Oszust");
    endif
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckHairs(who)

  var Race := GetCharRace(who);

  if(who.cmdlevel or !Race)
    return;
  endif
  
  if(GetObjProperty(who, "NoHairGrow"))
	return;
  endif

  var Hair := GetEquipmentByLayer(who, LAYER_HAIR);

  if(cfgRaces[Race].HairGrowTime)
  
    if(Hair.movable)
      Hair.movable := 0;
    endif

    if(!CInt(GetObjProperty(who,"HairGrow")))
      var onTime := GetObjProperty(who,PROP_ONLINETIMER);
      var HairTime := CInt(cfgRaces[Race].HairGrowTime);

      onTime := HairTime - (onTime - (HairTime * CInt(onTime / HairTime)));
      SetObjProperty(who,"HairGrow",GetObjProperty(who,PROP_ONLINETIMER) + onTime);
    endif

    if(GetObjProperty(who,"HairGrow") < GetObjProperty(who,PROP_ONLINETIMER) and CanMod(who, MOD_HAIRS))
      ChangeHairs(who, Race);
      SetObjProperty(who,"HairGrow",GetObjProperty(who,PROP_ONLINETIMER) + GetHairGrowTime(who));
    endif

  elseif(Hair)
    DestroyItem(Hair);
  endif

  var Beard := GetEquipmentByLayer(who, LAYER_BEARD);
  
  if(cfgRaces[Race].BeardGrowTime)

    if(Beard.movable)
      Beard.movable := 0;
    endif

    if(!CInt(GetObjProperty(who,"BeardGrow")))
      var onTime := GetObjProperty(who,PROP_ONLINETIMER);
      var BeardTime := CInt(cfgRaces[Race].BeardGrowTime);
      onTime := BeardTime - (onTime - (BeardTime * CInt(onTime / BeardTime)));
      SetObjProperty(who,"BeardGrow",GetObjProperty(who,PROP_ONLINETIMER) + onTime);
    endif

    if(GetObjProperty(who,"BeardGrow") < GetObjProperty(who,PROP_ONLINETIMER) and CanMod(who, MOD_HAIRS))
      ChangeBeard(who);
      SetObjProperty(who,"BeardGrow",GetObjProperty(who,PROP_ONLINETIMER) + GetBeardGrowTime(who));
    endif

  elseif(Beard)
    DestroyItem(Beard);
  endif

endfunction

//-----------------------------------------------------------------------------

function ChangeHairs(who, Race)

  var Hairs   := GetEquipmentByLayer(who,LAYER_HAIR);

  var NewHair := 0;
  var Color   := CInt(Hairs.color);

  if(!Hairs)
    NewHair := CInt(cfgRaces[Race].ShortHairs);
  else
    NewHair := CInt(cfgRaces[Race].LongHairs);
  endif

  if(NewHair)
    NewHair := CreateItemAtLoc(6109,1185,0,NewHair);
    if(NewHair)
      if(Hairs)
        if(!DestroyItem(Hairs))
          DestroyItem(NewHair);
        elseif(!EquipItem(who,NewHair))
          DestroyItem(NewHair);
        endif
      elseif(!EquipItem(who,NewHair))
        DestroyItem(NewHair);
      endif
    endif
  elseif(Hairs)
    DestroyItem(Hairs);
  endif

  if(NewHair)
    NewHair.color := Color;
    NewHair.movable := 0;
  endif

endfunction

//-----------------------------------------------------------------------------

function ChangeBeard(who)

  if(!who.gender and !IsVampire(who))

    var Beard    := GetEquipmentByLayer(who,LAYER_BEARD);
    var NewBeard := 0;
    var Color    := CInt(Beard.color);

    if(!Beard)
      NewBeard := UOBJ_MUSTACH;
    elseif(Beard.objtype in GetMustach())
      NewBeard := UOBJ_SHORT_BEARD_MUS;
    elseif(Beard.objtype in GetShortBeards() or Beard.objtype == UOBJ_LONG_BEARD)
      NewBeard := UOBJ_LONG_BEARD_MUS;
    endif

    if(NewBeard)
      NewBeard := CreateItemAtLoc(6109,1185,0,NewBeard);
      if(NewBeard)
        if(Beard)
          if(!DestroyItem(Beard))
            DestroyItem(NewBeard);
          elseif(!EquipItem(who,NewBeard))
            DestroyItem(NewBeard);
          endif
        elseif(!EquipItem(who,NewBeard))
          DestroyItem(NewBeard);
        endif
      endif

      if(NewBeard)
        NewBeard.color := Color;
        NewBeard.movable := 0;
      endif
    endif

  endif

endfunction

//-----------------------------------------------------------------------------

function MurdererFall(who)
  if(!who.murderer and !who.cmdlevel)    
    var longmurder := GetObjProperty(who,"longmurders");
    if(longmurder > 0)
      SetObjProperty(who,"longmurders", longmurder - 1);
      SetObjProperty(who,"blockfallmurderer", ReadGameClock() + HOW_LONG_WITHOUT_KILL);
    endif
  endif
endfunction


//-----------------------------------------------------------------------------
/*
  __NONPVP__ DELETED
function CheckPvPStatus(who)
  if(!IsYoung(who))
    if(CanChangePvPStatus(who))
      if(IsInNonPvPArea(who.x,who.y,who.realm))
        SetPvPStatus(who,PVP_NO);
      else
        SetPvPStatus(who,PVP_YES);
      endif
    endif
  else
    SetPvPStatus(who,PVP_NO);
  endif
endfunction
*/
//-----------------------------------------------------------------------------










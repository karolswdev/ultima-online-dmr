//-----------------------------------------------------------------------------

include ":magic:magic";
include "include/portals";
include "include/gumpboxes";
include "include/move";
include "include/items/containers";
include "runebook";
include "include/buildings";

include ":waypoints:waypoints";

const RECALL_SPELLID := 32;
const GATE_SPELLID := 52;

//-----------------------------------------------------------------------------

var _RuneDefs;

//-----------------------------------------------------------------------------
//  var maxcharges,charges,v,i,w,h,_RuneDefs,Desc,line1,line2,uses;

program RuneBook(who, book)

  if(!StandardItemCheck(who,book))
    return;
  endif

  _RuneDefs := GetObjProperty(book, "runedefs");

  if(!_RuneDefs)
    _RuneDefs := {};
    SetObjProperty(book,"runedefs",_RuneDefs);
  endif

  var Res := SendRuneBookGump(who, book);

  while(IsLoggedIn(who))

    if(!Res)
      return;
    elseif(Res == 900)
      RenameBook(who,book);
      return;
    elseif(Res >= 700)
      GateTravel(who,book,Res - 700);
      return;
    elseif(Res >= 600)
      TakeOutRune(who,book,Res - 600);
      return;
    elseif(Res >= 500)
      Recall(who,book,Res - 500);
      return;
    elseif(Res == 100)
      Res := SendRuneBookGump(who, book);
    elseif(Res <= 16)
      Res := ShowPages(who, book, Res);
    endif
    
  endwhile
  
endprogram

//-----------------------------------------------------------------------------

function SendRuneBookGump(who, book)

  RbGumpInit(book);
  GButton(  55,10,2472,2473,900);
  GTextLine(85, 12,0,"Zmien nazwe");

  var w := 0;
  var i := 1;
  var h := 0;

  foreach runed in _RuneDefs
    if(i == 9)
      w := 156;
      h := 0;
    endif

    var Desc := CLStr(runed[1]);
    GTextCrop(73+w,62+(h*15),120,20,0,Desc);
    GButton(56+w,65+(h*15),2118,2117,i+500);
    i := i + 1;
    h := h + 1;
  endforeach

  if(_RuneDefs.size())
    GButton(320,8,2236,2236,1);
  endif
  
  return GSend(who)[0];

endfunction

//-----------------------------------------------------------------------------

function RbGumpInit(book)

  GInit(30,30);
  GPage();
  GGumpPic(0,0,2220);
  GAddPageData(50,8,2235,2235,320,8,2236,2236);

  GGumpPic(56 ,42,57);
  GGumpPic(210,42,57);

  GGumpPic(163,42,59);
  GGumpPic(320,42,59);

  var i;

  for(i:=74;i<=150;i:=i+15)
    GGumpPic(i,42,58);
    GGumpPic(155+i,42,58);
  endfor

  GTextLine(85, 30,0,"Ladunki:");
  GTextLine(225,30,0,"Max ladunki:");
  GTextLine(150,30,0, GetRuneBookCharges(book));
  GTextLine(333,30,0, GetRuneBookMaxCharges(book));
  GTextLine(235,12,0, GetRuneBookUses(book)+" uzyc");

  GPage();

endfunction
  
//-----------------------------------------------------------------------------

function ShowPages(who, book, Nr)

  RbGumpInit(book);

  if(_RuneDefs[Nr+2])
    GButton(320,8,2236,2236,Nr+2);
  endif

  var w := 0;
  var i;

  for(i:=Nr;i<=Nr+1;i:=i+1)
    if(_RuneDefs[i])
      if(i == 1)
        GButton(50,8,2235,2235,100);
      elseif(i % 2 > 0)
        GButton(50,8,2235,2235,Nr-2);
      endif
      
      var Desc  := CLStr(_RuneDefs[i][1]);
      var line1 := Desc;
      var line2 := "";

      if( len(Desc) > 16 )
        Desc := SplitWords(Desc);
        if( (len(Desc[1]) + len(Desc[2]) + len(Desc[3]) + 2) <= 16 )
          line1 := ClStr(Desc[1]) + " " + ClStr(Desc[2]) + " " + ClStr(Desc[3]);
          line2 := ClStr(Desc[4]) + " " + ClStr(Desc[5]);
        elseif( len(Desc[1]) + len(Desc[2]) + 1 <= 12 )
          line1 := ClStr(Desc[1]) + " " + ClStr(Desc[2]);
          line2 := ClStr(Desc[3]) + " " + ClStr(Desc[4]) + " " + ClStr(Desc[5]);
        elseif( len(Desc[1]) <= 16 )
          line1 := ClStr(Desc[1]);
          line2 := ClStr(Desc[2]) + " " + ClStr(Desc[3]) + " " + ClStr(Desc[4]) + " " + ClStr(Desc[5]);
        endif
      endif
      
      GButton(62+w  ,129,2438,2437,i+600);
      GButton(70+w  ,150,2271,2271,i+500);
      GButton(130+w ,150,2291,2291,i+700);
      GTextCrop(60+w,55 ,130,20,0,line1);
      GTextCrop(60+w,75 ,130,20,0,line2);
      
      var Rlm := _RuneDefs[i][5];
      if(!Rlm)
        Rlm := _DEFAULT_REALM;
      endif
      
      GTextLine(54+w,100,0,ClStr(ToSextant(_RuneDefs[i][2], _RuneDefs[i][3],Rlm)));
      GTextLine(82+w,125,0,"Wyjmij rune");

      if(!w)
        w := 156;
      else
        w := 0;
      endif
    endif
  endfor

  return GSend(who)[0];

endfunction

//-----------------------------------------------------------------------------

function TakeOutRune(who,book,nr)

  var runeinfo := _RuneDefs[nr];
  if(!runeinfo)
    SendSysMessage(who, "Te runo juz nie istnieje.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  var rune := CreateItemInPack(who, UOBJ_RUNE);
  SetName(rune, ClStr(runeinfo[1]));
  SetObjProperty(rune,"x", runeinfo[2]);
  SetObjProperty(rune,"y", runeinfo[3]);
  SetObjProperty(rune,"z", runeinfo[4]);
  SetObjProperty(rune,"Rlm", runeinfo[5]);
  _RuneDefs.erase(nr);
  SetObjProperty(book,"runedefs", _RuneDefs);
  SendSysMessage(who,"Wyjal"+ggm(who,3)+" runo.",FONT_NORMAL,COLOR_GREEN);

endfunction

//-----------------------------------------------------------------------------

function RenameBook(who, book)
  var NewName := SendTextEntryGump( who, "Wpisz nowa nazwe ksiazki. ", TE_CANCEL_ENABLE, TE_STYLE_NORMAL);
  if(!NewName)
    SendSysMessage(who,"Anulowano",FONT_NORMAL,COLOR_RED);
    return;
  endif
  SetObjProperty(book,"name", NewName);
  UpDateBookName(book);
endfunction

//-----------------------------------------------------------------------------

function Recall(Caster,book,nr)

  /*
  var Res := IsSpellLearned(Caster, RECALL_SPELLID);

  if(!Res)
    SendSysMessage(Caster,Res.errortext,FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(!CanUseWaypoint(Caster))
    SendSysMessage(Caster,"Nie uda Ci sie tak czesto odwolywac. Musisz odczekac chwile.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  */

  var charges := GetRuneBookCharges(book);

  if(!charges)
    Fizzle(Caster,"Ta ksiazka nie jest naladowana.");
    return;
  endif

  var Loc        := {};
  Loc.+x         := _RuneDefs[nr][2];
  Loc.+y         := _RuneDefs[nr][3];
  Loc.+z         := _RuneDefs[nr][4];
  
  var Realm      := _RuneDefs[nr][5];
  
  if(Caster.x == Loc.x and Caster.y == Loc.y and Caster.realm == Realm)
    Fizzle(Caster, "Znajdujesz sie w miejscu docelowym.");
    return;
  endif

  if(!Realm)
    Realm := _DEFAULT_REALM;
  endif
  
  PrintTextAbove(Caster,"Kal Ort Por");

  var SpellParms := {};
  SpellParms[1]  := Caster;
  SpellParms[2]  := CAST_WAND;
  SpellParms[3]  := book;
  SpellParms[4]  := SPF_NOREGS | SPF_NODELAY | SPF_NOWORDS | SPF_RETCASTON;

  
  if(Realm == "britannia_alt" and Caster.realm != "britannia_alt")
    Fizzle(Caster, "Runa stracila swa moc.");
    return;
  endif

  if(NoTeleportZone(Caster.x, Caster.y, Caster.z, Caster.realm, TCF_NOCHECK_DUNG | TCF_NOCHECK_BLD | TCF_CHECK_CHAMP | TCF_NOCHECK_MULTI))
    Fizzle(Caster,"Nie mozesz stad sie odwolac.");
    return;
  endif

  if(NoTeleportZone(Loc.x,Loc.y,Loc.z,Realm))
    Fizzle(Caster, "Nie mozesz odwolac sie w to miejsce.");
    return;
  endif

  if(coordist(Loc.x, Loc.y, Caster.x, Caster.y) < 15)
    Fizzle(Caster, "Jestes juz w poblizu tego miejsca.");
    return;
  endif

  /*
  if(Realm != Caster.realm and (Realm == BRITA_REALM or Caster.realm == BRITA_REALM))
    Fizzle(Caster, "Nie mozesz odwolac sie w to miejsce.");
    return;
  endif
  */

  if(Caster.frozen or Caster.paralyzed)
    Fizzle(Caster, "Nie mozesz sie ruszyc.");
    return;
  endif

  if(Caster.hidden)
    Caster.hidden := 0;
  endif

  var Cast       := SpellCast(SpellParms, RECALL_SPELLID);
  if(!Cast)
    return;
  endif

  var lDist      := 15;

  if(Realm == Caster.realm)
    lDist := max(1, CInt(ceil(CDbl(coordist(Loc.x, Loc.y, Caster.x, Caster.y))/400.0)));
  endif

  var SpellsCfg := ReadConfigFile(SPELLS_CFG);
  
  /*
  var Reagents  := GetCOnfigStringArray(SpellsCfg[RECALL_SPELLID], "BReagent");

  if(!EatReagents(Caster, Reagents, lDist))
    Fizzle(Caster, "Brakuje Ci skladnikow!");
    SendReagentsList(Caster, Reagents, lDist);
    return;
  endif
  
  */

  var Cx := Caster.x,Cy := Caster.y,Cz := Caster.z;
  var ORlm := Caster.realm;
  
  MoveObject(Caster, Loc.x,Loc.y,Loc.z, Realm);
  PlayStationaryEff( Cx, Cy, Cz, FX_TELE_VANISH, 1, 30,0, ORlm);
  PlayStationaryEff( Loc.x,Loc.y,Loc.z, FX_TELE_VANISH, 1, 30,0, Realm);
  
  PlaySoundEffect(Caster, 0x1FD);
  charges := charges - 1;
  SetObjProperty(book,"charges",charges);
  UpDateBookName(book);
  CheckOldness(Caster,book);

  //SetWaypointDelay(Caster);

endfunction

//-----------------------------------------------------------------------------

function GateTravel(Caster,book,nr)

  /*
  var Res := IsSpellLearned(Caster, GATE_SPELLID);

  if(!Res)
    SendSysMessage(Caster,Res.errortext,FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(!CanUseWaypoint(Caster))
    Fizzle(Caster,"Nie uda Ci sie tak czesto przywolywac portalu. Musisz odczekac chwile.");
    return;
  endif
  
  */

  var Loc := {};
  Loc.+x  := _RuneDefs[nr][2];
  Loc.+y  := _RuneDefs[nr][3];
  Loc.+z  := _RuneDefs[nr][4];
  var Realm := _RuneDefs[nr][5];

  var lDist      := 20;

  if(Realm == Caster.realm)
    lDist := max(1, CInt(ceil(CDbl(coordist(Loc.x, Loc.y, Caster.x, Caster.y))/300.0)));
  endif

  var SpellParms := {};
  SpellParms[1]  := Caster;
  SpellParms[2]  := CAST_WAND;
  SpellParms[3]  := book;
  SpellParms[4]  := SPF_NODELAY | SPF_RETCASTON | SPF_NOREGS;

  if(Realm == "britannia_alt" and Caster.realm != "britannia_alt")
    Fizzle(Caster, "Runa stracila swa moc.");
    return;
  endif

  if(NoTeleportZone(Loc.x,Loc.y,Loc.z,Realm))
    Fizzle(caster, "Nie mozesz przywolac portalu do tego miejsca.");
    return;
  elseif(NoTeleportZone(Caster.x,Caster.y,Caster.z,Caster.realm))
    Fizzle(caster, "Nie mozesz przywolac portalu z tego miejsca.");
    return;
    /*
  __NONPVP__ DELETED
  elseif(IsInNonPvPArea(Loc.x,Loc.y,Realm) != IsInNonPvPArea(Caster.x,Caster.y,Caster.realm))
    SendSysMessage(caster, "Nie mozna stawiac portali miedzy strefami.");
    return;*/
  endif

  if(IsWaterInRange(Loc.x,Loc.y,Loc.z,Realm, 10))
    Fizzle(caster, "Woda zakloca mozliwosc przywolania portalu w to miejsce.");
    return;
  elseif(IsWaterInRange(Caster.x,Caster.y,Caster.z,Caster.realm, 10))
    Fizzle(caster, "Nie mozesz przywolac portalu, jestes zbyt blisko wody.");
    return;
  endif

  if(coordist(Caster.x, Caster.y, Loc.x, Loc.y) < 20 and Caster.realm == Realm)
    Fizzle(Caster, "Znajdujesz sie w miejscu docelowym...");
    return;
  endif

  if(Caster.hidden)
    Caster.hidden := 0;
  endif

  var Cast       := SpellCast(SpellParms, GATE_SPELLID);
  if(!Cast)
    return;
  endif

  Detach();
  NoLongerBusy(Caster);
  CheckOldness(Caster,book);

  var SpellsCfg := ReadConfigFile(SPELLS_CFG);
  var Reagents  := GetCOnfigStringArray(SpellsCfg[GATE_SPELLID], "BReagent");

  if(!EatReagents(Caster, Reagents, lDist))
    SendReagentsList(Caster, Reagents, lDist);
    return;
  endif

  //SetWaypointDelay(Caster);
  CreateGateTravel(Caster.x,Caster.y,Caster.z,Caster.realm,Loc.x,Loc.y,Loc.z,Realm, CGTF_ONE_WAY, Caster);

endfunction

//-----------------------------------------------------------------------------

function SpellAction(Caster, CastOn)

  __ParamUnused(Caster);
  __ParamUnused(CastOn);

endfunction

//-----------------------------------------------------------------------------

function CheckOldness(who, book)

  var uses := GetRuneBookUses(book);
  var maxuses := GetRuneBookMaxUses(book);

  if(uses > maxuses and Randomint(100) < 15)
    DestroyRuneBook(who,book);
  endif

  uses := uses + 1;
  SetObjProperty(book,"Uses",uses);

  if(maxuses - uses < 5) 
    SendSysmessage(who, "Runebook wyglada na mocno zuzyty.",FONT_NORMAL,COLOR_RED);
  endif
  
endfunction

//-----------------------------------------------------------------------------

function DestroyRuneBook(who,book)

  var i,w;

  var maxcn := cint(len(_RuneDefs) / 4);
  if(maxcn < 1)
    maxcn := 1;
  endif

  for(i:=1;i<=maxcn;i:=i+1)
    w := RandomInt(len(_RuneDefs)+1);
    _RuneDefs.erase(w);
  endfor

  for(i := 1;i<=len(_RuneDefs);i:=i+1)
    TakeOutRune(who,book,i);
  endfor

  Sendsysmessage(who, "O nie! Runebook rozlecial Ci sie w rekach!",FONT_NORMAL,COLOR_RED);
  DestroyItem(book);
  exit;

endfunction

//-----------------------------------------------------------------------------

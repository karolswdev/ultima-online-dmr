//-----------------------------------------------------------------------------

use uo;

//-----------------------------------------------------------------------------

include "include/getobj";
include "include/items";
include "include/objtypes/misc";
include "include/proc";
include "include/misc";
include "include/events";
include "include/client/fonts";
include "include/checks/stdchecks";
include "include/coords";
include ":magicitems:common";
include "include/move";

include ":crafting:mat_groups";

//-----------------------------------------------------------------------------
var gGrab_MagicItem := 0;
var PackItm,Pass := 0,txt, who;
var WantObjTypes;
var ContainerId;

//-----------------------------------------------------------------------------

program GrabCmd(me,text)
  
  who := me;
  txt := text;
  GenWantObjTypes();
  var where;

  if (CheckIfSetBagId(me))
    where := FindItemBySerial(GetObjProperty(me, "grabSerialBag"));
  else
    where := me.backpack;
  endif

  if(txt)
    if(!txt["do"] and!txt["ziola"] and !txt["klejnoty"] and !txt["zloto"] and !txt["strzaly"] and !txt["belty"] and !txt["druid"] and !txt["nekro"] and !txt["wampir"] and !txt["wilkolak"]  and !txt["magiczne"] and !txt["specloot"] and  !(txt["fast"] and who.cmdlevel))
      SendSysMessage(me,".grab [do/ziola/klejnoty/zloto/strzaly/belty/druid/nekro/wampir/wilkolak/magiczne/specloot].",FONT_NORMAL,COLOR_RED);
    endif
  endif

  //wybor do plecaka
  if(txt["do"])
      SetGlobalGrabBagId(who);
      where := FindItemBySerial(GetObjProperty(me, "grabSerialBag"));
      return;
  endif
  //koniec wyboru

  if(me.dead)
    SendSysMessage(me,"Nie mozesz tego wziac ze soba!",FONT_NORMAL,COLOR_RED);
    EraseObjProperty(me, "#looting");
    return;
  endif

  if(ExPid(GetObjProperty(me, "#looting")))
    SendSysMessage(me, "Juz zbierasz przedmioty!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  SetObjProperty(me, "#looting",GetPid());

  foreach item in ListItemsNearLocation(me.x, me.y, me.z,1,me.realm)

    if(item.objtype == UOBJ_CORPSE or (item.objtype >= UOBJ_BONES_START and item.objtype <= UOBJ_BONES_END))
      if(!GetObjProperty(item,"AiSerial") and CInt(GetObjProperty(item,PROP_CORPSE_OWNER)) != me.serial)
        CheckGoodCaster(item,me);
      endif

      foreach thingie in ListRootItemsInContainer(item)
        if(me.dead)
          SendSysMessage(me,"Nie mozesz tego wziac ze soba!",FONT_NORMAL,COLOR_RED);
          EraseObjProperty(me, "#looting");
          return;
        endif
        if(Distance(me,item) > 1)
          SendSysMessage(me,"Jestes za daleko by zebrac przedmioty.",FONT_NORMAL,COLOR_RED);
          EraseObjProperty(me, "#looting");
          return;
        endif
        if(thingie.container == item)
          Grabit (me,thingie,where);
          Sleepms(1);
        endif
      endforeach

    elseif(item.movable and !item.container and DistZ(me, item) < 5)

      if(me.dead)
        SendSysMessage(me,"Nie mozesz tego wziac ze soba!",FONT_NORMAL,COLOR_RED);
        EraseObjProperty(me, "#looting");
        return;
      endif

      if(Distance( me, item ) > 1)
        SendSysMessage(me,"Jestes za daleko by zebrac przedmioty.",FONT_NORMAL,COLOR_RED);
        EraseObjProperty(me, "#looting");
        return;
      endif

      Grabit(me,item,where);
    endif

    Sleepms(1);
  endforeach

  if(Pass)
    SendSysMessage(me,"Hmm... wyglada na to, ze wzi"+ggm(me,5)+" juz wszystko.",FONT_NORMAL,COLOR_BLUE);
  else
    SendSysMessage(me,"Na ziemi nie ma nic co mogl"+ggm(me,4)+"bys zebrac.",FONT_NORMAL,COLOR_GREEN);
  endif

  EraseObjProperty(me, "#looting");

endprogram

//-----------------------------------------------------------------------------

Function GrabIt(me, it, where)

  if(!it.movable or !it)
    return;
  endif

  if(!Accessible(me, it) or IsHairs(it))
    return;
  endif
  
  if(!WantToGrab(it))
    return;
  endif

  if(MoveToCont(it, where, MOVECONT_JOIN_STACKS))

    Pass := 1;

    if(who.cmdlevel)
      if(txt["fast"])
        return;
      endif
    endif

    if(!IsStaff(me))
      PrintTextAbove (me,"*yoink*");
      sleepms(500);
    endif

  endif

endfunction

//-----------------------------------------------------------------------------

function CheckGoodCaster(item,who)

  foreach Mob in ListMobilesNearLocation(item.x,item.y,item.z,10,item.realm)
    if(Mob.script["good"])
      SendChEvent(Mob,who,EVID_ENGAGED);
    endif
  endforeach

endfunction

//-----------------------------------------------------------------------------

function WantToGrab(it)

  if(!WantObjTypes.size() and (gGrab_MagicItem == 0))
    return 1;
  else

    if ((gGrab_MagicItem == 1) and IsUndefinedMagicItem(it))
      return 1;
    endif

    return (it.objtype in WantObjTypes) ;

  endif

endfunction

//-----------------------------------------------------------------------------

function GenWantObjTypes()

  WantObjTypes := array;
  
  if(txt["ziola"])
    WantObjTypes := WantObjTypes + GetMainRegsObjTypes();
  endif
  
  if(txt["klejnoty"])
    WantObjTypes := WantObjTypes + GetGemsObjTypes();
  endif
  
  if(txt["zloto"])
    WantObjTypes.append(UOBJ_GOLDCOIN);
  endif
  
  if(txt["strzaly"])
    WantObjTypes.append(UOBJ_ARROW);
  endif

  if(txt["belty"])
    WantObjTypes.append(UOBJ_BOLTS);
  endif

  if(txt["druid"])
    WantObjTypes := WantObjTypes + GetDruidRegObjTypes();
  endif

  if(txt["nekro"])
    WantObjTypes := WantObjTypes + GetNecroRegObjTypes();
  endif

  if(txt["wampir"])
    WantObjTypes := WantObjTypes + GetVampireRegObjTypes();
  endif

  if(txt["wilkolak"])
    WantObjTypes := WantObjTypes + GetWearWolfRegObjTypes();
  endif
  
  if(txt["specloot"])
    WantObjTypes := WantObjTypes + GetSpecLootObjTypes();
  endif

  if(txt["magiczne"])
    gGrab_MagicItem := 1;
  endif

endfunction

//-----------------------------------------------------------------------------

function CheckIfSetBagId(who)
  //funkcja sprawdzic czy zosta³ zdefiniowany plecak do grabienia, jesli tak to sprawdzi czy ten plecak znajduje sie w glownym plecaku
  var item :=  FindItemBySerial (GetObjProperty(who, "grabSerialBag"));
  var topCont := GetTopCont(item);
  
  if(topCont == who.backpack)
    return 1;
  else
    EraseObjProperty(who, "grabSerialBag");
    return 0;
  endif
endfunction

//-----------------------------------------------------------------------------

function SetGlobalGrabBagId(who)

  SendSysMessage(who, "Do ktorego pojemnika chcesz grabic przedmioty?",FONT_NORMAL,COLOR_BLUE);
	var targ := target(who);

	if ( !targ )
	  SendSysMessage(who, "Anulowano",FONT_NORMAL,COLOR_RED);
	  return 0;
	endif
  //sprawdz czy target to kontener i ustaw jego id
	if ( targ.isa(POLCLASS_CONTAINER) )
    SendSysMessage(who, "Wybrano pojemnik",FONT_NORMAL,COLOR_BLUE);
		SetObjProperty(who, "grabSerialBag", targ.serial);
    return 1;
  else
		SendSysMessage(who, "To nie pojemnik.",FONT_NORMAL,COLOR_RED);
    return 0;
	endif
 
endfunction


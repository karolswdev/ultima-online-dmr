//-----------------------------------------------------------------------------

include "include/skills/skillwin";
include "include/clock";
include ":magic:magicatt";
include "include/player/misc";
include "include/checks/objchecks";
include "include/gumpboxes";
include "include/player/nutrition";
include "include/reputation/murderer";
include "include/reputation/penalty";
include "include/reputation/pvparea";
include "include/std";
include "include/mobile/age";
include "include/resurrect";
include ":jail:common";
include ":jail:jail";
include ":traits:gumps";
include ":musicianship:music";
include ":npcguilds:guilds";
include ":instances:instances";

//-----------------------------------------------------------------------------

const MAX_SKILLS_IN_SKWIN        := 20;
const DEC_SKILL_TIME_DIVIDE      := 5;
const DEC_STAT_TIME_DIVIDE       := 10;

//-----------------------------------------------------------------------------

var who, hidegump;

var SkInGump    := {};
var SkWinBuild  := 0;
var SkValOffset := 0;

//-----------------------------------------------------------------------------

program SkillWindow(_who)

  if(!IsCharCreationFinished(_who))
    if(GetPlayerLogonTime(_who) < 5)
      return; 
    endif
  endif

  if(ExPid(GetObjProperty(_who, "#SkillWinPid")))
    return;
  endif
  
  SetObjProperty(_who,"#SkillWinPid",GetPid());
  
  who             := _who;
  SkInGump        := GetSkillsInGump(who);

  BuildMainSkillGump();
  MainSkillWindow();

endprogram

//-----------------------------------------------------------------------------

function MainSkillWindow()
  
  var keeprun := 1;

  while(keeprun)
    var res    := GSend(who);

    if(IsVarType(res, VAR_STRUCT))
      res := res.butid;
    else
      res := res[0];
    endif

    if(!res or res <= 0)
      keeprun := 0;
    endif

    case(res)
      9999:    BuildAllSkillsWindow(who);
               SkWinBuild := 0;
               continue;
      9997:    ChangeMaxSkInGump();
               SkWinBuild := 0;
      9996:    BuildStatsWindow(who);
               SkWinBuild := 0;
               continue;
      9995:    hidegump := 1;
      9994:    ShowInfoGump(who);
               SkWinBuild := 0;
               continue;
      9993:    ShowBardGump(who);
               SkWinBuild := 0;
               continue;
      9992:    ShowTraitsGump(who);
               SkWinBuild := 0;
    endcase
    
    if(res >= 1 and res < 200)
      StartSkillScript((res-1));
    elseif(res >= 200 and res < 400)
      BlockSkill(who,(res-200));
      SkWinBuild := 0;
    elseif(res >= 400 and res < 600)
      ChangeSkillValue((res-400));
      SkWinBuild := 0;
    elseif(res >= 600 and res < 800)
      RemoveFromSkGump((res-600));
      SkWinBuild := 0;
    elseif(res >= 800 and res < 1000)
      AddToSkGump((res-800));
    elseif(res >= 1000 and res < 1010)
      ChangeBardSound(who,res-1000);
    elseif(res > 9900 and res < 9904)
      BlockStat(who,(res-9900));
    elseif(res > 9800 and res < 9804)
      ChangeStatValue((res-9800));
    endif

    if(hidegump)
      ShowHiddenGump();
      SkWinBuild := 0;
    else
      BuildMainSkillGump();
    endif

  endwhile

endfunction

//-----------------------------------------------------------------------------

function BuildMainSkillGump()

  if(!SkWinBuild)
    var MaxSkInGump := GetMaxSkInGump(who);
    SkInGump    := GetSkillsInGump(who);

    StandSkLayout(MaxSkInGump, 10);
    GPage();

    GButton(  180,1,  2093,2093,9995);
    GButton(  340,24, 1227,1227,9997);

    GTextLine(50, 30, 610,"Odswiez");
    GButton(  30, 34, 2117,2118,9998);
    GTextLine(140,30, 610,"Statystyki");
    GButton(  120,34, 2117,2118,9996);
    GTextLine(235,30, 610,"Info");
    GButton(  215,34, 2117,2118,9994);
    GTextLine(290,30, 610,"Cechy");
    GButton(  270,34, 2117,2118,9992);
 
    GButton(  30, (94+(MaxSkInGump*20)),2117,2118,9999);
    GTextLine(50, (90+(MaxSkInGump*20)),610,"Umiejetnosci");

    if(GetEffectiveSkill(who,SKILLID_MUSICIANSHIP) >= 60)
      GButton(  140,(94+(MaxSkInGump*20)),2117,2118,9993);
      GTextLine(160,(90+(MaxSkInGump*20)),610,"Bard");
    endif

    GTextLine(220,(90+(MaxSkInGump*20)),610,CountAllSkills(who,SkInGump));
    GTextLine(300,(90+(MaxSkInGump*20)),610,CountAllSkills(who));

    GAddPageData(365,55, 2650,2651,365,(63+(MaxSkInGump*20)),2648,2647);

    SkValOffset := GumpData.data.size();
    
    var IsBlocked := IsGainBlockTimed(who);

    foreach SkID in SkInGump
    
      GCheckNextPage(MaxSkInGump);
      var SkElem := GetSkillConfigElem(SkID);
    
      var SkVal := GetEffectiveSkillBaseValue(who, SkID);

      if(SkElem.NotVisible)
        GTextLine(70, Cint(67+ (GNPCount()*20)),1121,SkElem.name);
      elseif(IsBlocked)
        GTextLine(70, Cint(67+ (GNPCount()*20)),1209,SkElem.name);
      else
        GTextLine(70, Cint(67+ (GNPCount()*20)),550,SkElem.name);
      endif
      
      if(GetSkillBaseMaxValue(who, SkID) > DEF_SK_MAX_VALUE)
        GTextLine(270,Cint(67+ (GNPCount()*20)),__IfElse(SkElem.NotVisible or IsBlocked, 1209, 1320),SkVal*0.1);
      else
        GTextLine(270,Cint(67+ (GNPCount()*20)),__IfElse(SkElem.NotVisible or IsBlocked, 1209, 550),SkVal*0.1);
      endif

      GButton(  340,Cint(70+ (GNPCount()*20)),2117,2118,(400+SkID));
      GButton(  250,Cint(68+ (GNPCount()*20)),2706,2707,(600+SkID));
      if(SkElem.script and SkElem.script != "noskill")
        GButton(50, Cint(73+(GNPCount()*20)),2103,2104,(SkID+1));
      endif
      if(IsSetAttrFlag(who, GetAttributeIdBySkillId(SkID), ATTRF_BLOCKED))
        GButton(320,Cint(72+(GNPCount()*20)),2360,2361,(200+SkID));
      else
        GButton(320,Cint(72+(GNPCount()*20)),2362,2360,(200+SkID));
      endif
    endforeach

    SkWinBuild := ReadGameClock();

  elseif(SkWinBuild + 2 <= ReadGameClock())

    GumpData.data[SkValOffset-1] := CountAllSkills(who, SkInGump);
    GumpData.data[SkValOffset]   := CountAllSkills(who);

    foreach SkID in SkInGump
      GumpData.data[SkValOffset+_SkID_iter*2] := GetEffectiveSkillBaseValue(who,(SkID))*0.1;
    endforeach

    SkWinBuild := ReadGameClock();
  endif
 
endfunction

//-----------------------------------------------------------------------------

function GroupSkills(byref Skills)

  var Groups := dictionary;
  var SkElem, AttrGroup;

  foreach SkId in Skills
    SkElem := GetSkillConfigElem(SkId);
    AttrGroup := GetAttributeGroupConfig(SkElem.AttrGroup);

    if(!Groups.exists(AttrGroup.Name))
      Groups[AttrGroup.Name] := dictionary;
    endif

    Groups[AttrGroup.Name][SkId] := SkElem;

  endforeach

  return Groups;

endfunction

//-----------------------------------------------------------------------------

function ShowHiddenGump()

  GInit();
  GButton(20,20,2105,2105,9998);
  SkInGump         := {};
  hidegump         := 0;

endfunction

//-----------------------------------------------------------------------------

function BuildStatsWindow(who)

  StandSkLayout(5);
  GTextLine(50, 30, 610,"Odswiez");
  GButton(  30, 34, 2117,2118,9996);
  GButton(130,34,2117,2118,9998);
  GTextLine(150,30,610,"Umiejetnosci");
  
  var aprilfool := CInt(GetObjProperty(who,"#modstatfalse"));
  GTextLine(70, 87, 550,"Sila");
  GTextLine(260,87, 550,GetBaseStrengthHundrethValue(who)*0.01 + __ifelse(aprilfool==1,1,0));

  GButton(  340,91, 2117,2118,9801);

  if(IsSetAttrFlag(who, ATTRIBUTEID_STRENGTH, ATTRF_BLOCKED))
    GButton(320,92 ,2360,2361,9901);
  else
    GButton(320,92 ,2362,2360,9901);
  endif

  GTextLine(70, 107,550,"Zrecznosc");
  GTextLine(260,107,550,GetBaseDexterityHundrethValue(who)*0.01 + __ifelse(aprilfool==2,1,0));
  GButton(  340,111,2117,2118,9802);

  if(IsSetAttrFlag(who, ATTRIBUTEID_DEXTERITY, ATTRF_BLOCKED))
    GButton(320,112,2360,2361,9902);
  else
    GButton(320,112,2362,2360,9902);
  endif

  GTextLine(70, 127,550,"Inteligencja");
  GTextLine(260,127,550,GetBaseIntelligenceHundrethValue(who)*0.01 + __ifelse(aprilfool==3,1,0));
  GButton(  340,131,2117,2118,9803);

  if(IsSetAttrFlag(who, ATTRIBUTEID_INTELLIGENCE, ATTRF_BLOCKED))
    GButton(320,132,2360,2361,9903);
  else
    GButton(320,132,2362,2360,9903);
  endif

endfunction

//-----------------------------------------------------------------------------

function ShowInfoGump(who)

  var List := GetInfoList(who);

  StandSkLayout(11+List.size(), 15);
  GTextLine(50, 30, 610,"Odswiez");
  GButton(  30, 34, 2117,2118,9994);
  GButton(  130,34, 2117,2118,9998);
  GTextLine(150,30, 610,"Umiejetnosci");

  GTextLine(60, 72, 999,"Zyjesz juz na tym swiecie");
  GTextLine(230,72, 550,GetAgeDesc(who));

  var CapColor;
  var SkSum := GetObjProperty(who, PROP_SKILLSUM);
  var StatSum := GetObjProperty(who, PROP_STATSUM);

  if(GetObjProperty(who, PROP_SKILLCAP))
    CapColor := 1211;
  else
    CapColor := 1420;
  endif
  
  GTextLine(95, 97, 999,"Maks umiejetnosci");
  GTextLine(210,97, CapColor, ((SkSum[1] + Max(SkSum[2] - SK_CRAFT_SKILLCAP_ADD, 0)) * 0.1));
  GTextLine(250,97, 560, "/ "+(who.skillcap - SK_CRAFT_SKILLCAP_ADD/10));

  if(SkSum[2] >= SK_CRAFT_SKILLCAP_ADD)
    CapColor := 1211;
  else
    CapColor := 1420;
  endif
  
  GTextLine(95, 117, 999,"Dodatek rzemiosla");
  GTextLine(210,117, CapColor, Min(((SkSum[2]) * 0.1), SK_CRAFT_SKILLCAP_ADD/10));
  GTextLine(250,117, 560, "/ "+SK_CRAFT_SKILLCAP_ADD/10);

  /*if(GetObjProperty(who, PROP_SKILLCAP))
    GGumpPic(280,102,2360);
  else
    GGumpPic(280,102,2361);
  endif*/
  if(GetObjProperty(who, PROP_STATCAP))
    CapColor := 1211;
  else
    CapColor := 1420;
  endif

  GTextLine(110 ,137,999,"Maks statystyk");
  GTextLine(210,137, CapColor, (StatSum * 0.1));
  GTextLine(250,137, 560, "/ "+who.statcap);
  /*if(GetObjProperty(who, PROP_STATCAP))
    GGumpPic(280,122,2360);
  else
    GGumpPic(280,122,2361);
  endif*/

  GTextLine(60 ,157,999, "Twoj maksymalny udzwig");
  GTextLine(210,157,560, GetFullWeight(who));

  GTextLine(110, 187, 999, "Glod:");
  GTextLine(140,187, 560, GetNutrValState(GetNutrValue(who, NVI_HUNGER)));
  GTextLine(100, 207, 999, "Bialka:");
  GTextLine(140,207, 560, GetNutrValState(GetNutrValue(who, NVI_PROTEIN)));
  GTextLine(80, 227, 999, "Witaminy:");
  GTextLine(140,227, 560, GetNutrValState(GetNutrValue(who, NVI_VITAMIN)));
  GTextLine(60, 247, 999, "Weglowodany:");
  GTextLine(140,247, 560, GetNutrValState(GetNutrValue(who, NVI_SUGAR)));
  
  var lPos := 277;

  foreach Entry in List
    if(Entry[4])
      GTextLine(Entry[1],lPos,999,Entry[2]);
      GTextLine(215,lPos,Entry[3],Entry[4]);
    else
      GTextLine(80,lPos,Entry[3],Entry[2]);
    endif
    lPos += 20;
  endforeach

endfunction

//-----------------------------------------------------------------------------

function GetInfoList(who)

  var List := array;

  if (IsGainBlockTimed(who))
    List.append(array(68, "Czas blokady przyrostu:", 1211, DescGameTime(GetObjProperty(who, PROP_GAIN_BLOCK) - ReadGameClock()) ));
  endif

  var TimeLeft := CInt(GetObjProperty(who, "#PvpAreaLeft"));

  if(TimeLeft + PVPAREA_LEFT_DELAY > ReadGameClock())
    List.append(array(55, "Przyjecie zasad porzadku:" ,1211, DescGameTime((TimeLeft+PVPAREA_LEFT_DELAY) - ReadGameClock()) ));
  endif
  
  if(IsJailed(who))
    var JailProps := GetJailProps(who);
    List.append(array(100, "Pobyt w wiezieniu:", 1209, DescGameTime(JailProps[2] + JailProps[3] - ReadGameClock()) ));
  endif

  if (IsWeakSoul(who))
    List.append(array(135, "Slaba dusza:", 560, DescGameTime(  GetObjProperty(who,"ResStopTimer") - ReadGameClock()) ));
  endif
  
  if(!CheckInstanceBan(who))
	var remainingTime := ReadGameClock() - CInt(GetObjProperty(who, "LastInstanceEntry"));
	List.append(array(60, "Blokada instancji:", 33, DescGameTime(remainingTime)));
  endif
  
  var offlineGain := GetObjProperty(who, "OfflineGain");
  
  if(offlineGain != error)
	if(ReadGameClock() < CInt(offlineGain))
		var gainTime := CInt(offlineGain) - ReadGameClock();
		List.append(array(60, "Bonus czasu offline:", 560, DescGameTime(gainTime)));
	endif
  endif
  
  var skPotion := GetObjProperty(who, "SkillPotion");
  
  if(skPotion)
	var skPotionLength := CInt(GetObjProperty(who, "SkillPotion"));
	if(ReadGameClock() < skPotionLength)
		var timeRemaining := skPotionLength - ReadGameClock();
		List.append(array(60, "Dzialanie mikstury nauki:", 560, DescGameTime(timeRemaining)));
	endif
  endif
  
  var instancePoints := CInt(GetObjProperty(who, "HonorPoints"));
  
  if(instancePoints > 0)
	List.append(array(60, "Ilosc punktow instancji:", 560, instancePoints));
  endif
		

  var MurdData := GetObjProperty(who, PROP_MURDPNDATA);

  if(MurdData.LastKill and MurdData.LastKill + MP_DELAY_TIME > GetObjProperty(who, PROP_ONLINETIMER))
    List.append(array(93, "Czas kary za atak:", 560, DescGameTime(  (MurdData.LastKill + MP_DELAY_TIME) - GetObjProperty(who, PROP_ONLINETIMER)) ));
  endif

  var MurdCount := "",MurdText := "";
  if(GetMurdersCount(who) > 4)
    MurdCount := CInt(CInt(GetObjProperty(who, "decaylongcountat")) - GetObjProperty(who, PROP_ONLINETIMER));
    if(MurdCount > 0)
      MurdCount := DescGameTime(MurdCount);
      MurdText  := "Czas bycia morderca:";
    endif
  elseif(GetShortMurdersCount(who) > 0)
    MurdCount := CInt(CInt(GetObjProperty(who, "decayshortcountat")) - GetObjProperty(who, PROP_ONLINETIMER));
    if(MurdCount > 0)
      MurdCount := DescGameTime(MurdCount);
      MurdText  := "Czas bycia morderca:";
    endif
  elseif(who.criminal)
    MurdText := "Jestes kryminalista.";
  endif

  if(MurdText)
    List.append(array(83, MurdText, 560, MurdCount));
  endif
  
  var MurdDesc := MurdererDescription(GetObjProperty(who,"longmurders"));

  if(MurdDesc)
    List.append(array(60, MurdDesc, 560, "" ));
  endif
  
  if(IsReflected(who))
    List.append(array(60, "Jestes chronion"+ggm(who,6)+" przed magia", 560, "") );
  endif

  var Fights := GetAcceptedFights(who);

  foreach Fighter in Fights
    List.append(array(60, "Pojedynek z "+GetName(Fighter), 560, ""));
  endforeach


    var PlayerData := GetObjProperty(who, PROP_NGDDATA);
    var GuildData := GetNGdData();
    var GdIds := array;
    var GdTitles := array;
    var GdNames := array;

    foreach GdData in (PlayerData.Guilds)
      if(GdData.Flags & NGPF_MEMBER)
        var Guild := GuildData.FindElement(NGUILD_TITLE+_GdData_iter);
        var Title := GetNpcGuildTitle(Guild.GetProp(GDPROP_TYPE), GdData.Level);
        var Name := Guild.GetProp(GDPROP_NAME);

        GdIds.append(_GdData_iter);
        GdTitles.append(Title);
        GdNames.append(Name);
      endif
    endforeach
    
    
      
    foreach Title in GdTitles
          List.append(array(60,"Gildia: "+_Title_iter+". "+GdNames[_Title_iter]+", "+Title,560, ""));
    endforeach

  
  return List;

endfunction

//-----------------------------------------------------------------------------

function ShowBardGump(who)

  StandSkLayout(6);
  GButton(  120,34, 2117,2118,9998);
  GTextLine(170,30, 610,"Umiejetnosci");

  GTextLine(70, 77,560,"Jak chcesz grac:");

  GTextLine(70, 107,550,"Normalnie");
  
  if(GetSavedSound(who) == S_SUCCESS_ID)
    GButton(300,112,2361,2361,1001);
  else
    GButton(300,112,2362,2361,1001);
  endif

  GTextLine(70, 127,550,"Spokojnie");

  if(GetSavedSound(who) == S_PEACE_ID)
    GButton(300,132,2361,2361,1002);
  else
    GButton(300,132,2362,2361,1002);
  endif
  
  if(GetEffectiveSkill(who,SKILLID_MUSICIANSHIP) >= 65)
    GTextLine(70, 147,550,"Dramatycznie");

    if(GetSavedSound(who) == S_DRAMA_ID)
      GButton(300,152,2361,2361,1003);
    else
      GButton(300,152,2362,2361,1003);
    endif
  endif
  
  if(GetEffectiveSkill(who,SKILLID_MUSICIANSHIP) >= 70)
    GTextLine(70, 167,550,"Walecznie");

    if(GetSavedSound(who) == S_BATTLE_ID)
      GButton(300,172,2361,2361,1004);
    else
      GButton(300,172,2362,2361,1004);
    endif
  endif
  
endfunction

//-----------------------------------------------------------------------------

function AddToSkGump(SkID)

  if(SkID in SkInGump)
    return 0;
  elseif(len(SkInGump) >= MAX_SKILLS_IN_SKWIN)
    SendSysMessage(who,"Nie mozesz dodac wiecej umiejetnosci.",FONT_NORMAL,COLOR_RED);
    return 0;
  else
    SkInGump.append(SkID);
    SetObjProperty(who,"SkillsInSkWin",SkInGump);
  endif
  
  return 1;

endfunction

//-----------------------------------------------------------------------------

function RemoveFromSkGump(SkID)

  if(!(SkID in SkInGump))
    return;
  else
    SkInGump.erase(retindex(SkInGump,SkID));
    SetObjProperty(who,"SkillsInSkWin",SkInGump);
  endif
  
  return 1;

endfunction

//-----------------------------------------------------------------------------

function GetMaxSkInGump(who)

  var MaxSk := GetObjProperty(who,"MaxSkInSkWin");
  if(!MaxSk)
    MaxSk := MAX_SKILLS_IN_SKWIN;
    SetObjProperty(who,"MaxSkInSkWin",MaxSk);
  endif

  return Cint(MaxSk);

endfunction

//-----------------------------------------------------------------------------

function ChangeMaxSkInGump()

  var MaxSk := GetObjProperty(who,"MaxSkInSkWin");
  if(!MaxSk)
    MaxSk := 20;
    SetObjProperty(who,"MaxSkInSkWin",MaxSk);
  endif
  
  MaxSk := MaxSk + 5;
  if(MaxSk > MAX_SKILLS_IN_SKWIN)
    MaxSk := 5;
  endif
  
  SetObjProperty(who,"MaxSkInSkWin",MaxSk);

endfunction

//-----------------------------------------------------------------------------

function StartSkillScript(SkID)

  if(who.frozen)
    SendSysMessage(who,"Jestes zamrozon"+ggm(who,6)+" i nie uda Ci sie tego zrobic!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(who.paralyzed)
    SendSysMessage(who,"Jestes sparalizowan"+ggm(who,6)+" i nie uda Ci sie tego zrobic!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(who.dead)
    SendSysMessage(who,"Jestes martw"+ggm(who,6)+"!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  var SkElem := GetSkillConfigElem(SkID);
  
  if(who.hidden and SkElem.UnHides != 0)
    who.hidden := 0;
  endif
  
  start_script(CStr(SkElem.script),who);
  sleepms(200);

endfunction

function UseHonorScroll(skill)
  
  var hs := FindObjTypeInContainer(who.backpack, 0xe34);
  if(hs and hs.color == 1158)

	var serial := GetObjProperty(hs, "OwnerSerial");
	if(serial != error)
		if(CInt(serial) != who.serial)
			SendSysMessage(who, "Ten zwoj nie nalezy do Ciebie.", FONT_NORMAL, COLOR_RED);
			return 0;
		return;
		endif
	endif
	
	var crBy := GetObjProperty(hs, "CreatedBy");
	if(crBy != error)
		if(CInt(crBy) != who.serial)
			SendSysMessage(who, "Ten zwoj nie nalezy do Ciebie.", FONT_NORMAL, COLOR_RED);
			return 0;
		endif
	endif

	if(GetBaseSkillBaseValue(who, skill) > 1000)
	    SendSysMessage(who, "Nie mozesz powiekszyc tej umiejetnosci.", FONT_NORMAL, COLOR_RED);
	    return 0;
	endif
	
	var unblockSkill := 0;
	
	if(GetBaseSkillBaseValue(who, skill) == 1000)
		if(YesNo(who, "Na pewno odblokowac nauke tej umiejetnosci do 115%?"))
			unblockSkill := 1;
		endif
	endif

	if(YesNo(who, "Na pewno uzyc Zwoj Zaslugi na te umiejetnosc?"))
		if(DestroyItem(hs))
			if(unblockSkill)
				if(SetMaxSkillValue(who, skill, 1150, MSKT_CLASS))
					SendSysMessage(who, "Odblokowano te umiejetnosc.", FONT_NORMAL, COLOR_BLUE);
					return 1;
				else
					SendSysMessage(who, "Nie udalo sie odblokowac tej umiejetnosci.", FONT_NORMAL, COLOR_RED);
					return 1;
				endif
			else
				var currentSkill := GetBaseSKillBaseValue(who, skill);
				
				if(currentSkill+30 > 1030)
					SetBaseSkillBaseValue(who, skill, 1000);
				else
					SetBaseSkillBaseValue(who, skill, currentSkill+30);
				endif
				
				return 1;
			endif
		endif
	endif
   endif

return 0;

endfunction

//-----------------------------------------------------------------------------

function ChangeSkillValue(skill)

  if(who.frozen)
    SendSysMessage(who,"Jestes zamrozon"+ggm(who,6)+" i nie uda Ci sie tego zrobic!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(who.paralyzed)
    SendSysMessage(who,"Jestes sparalizowan"+ggm(who,6)+" i nie uda Ci sie tego zrobic!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(who.dead)
    SendSysMessage(who,"Jestes martw"+ggm(who,6)+"!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(IsBusy(who, NO_BUSY_SET))
    return;
  endif

  if(UseHonorScroll(skill))
	return;
  endif
  
  SendSysMessage(who,"Do jakiej wartosci chcesz zapomniec umiejetnosc: "+GetSkillConfigElem(skill).Name+" ?",FONT_NORMAL,COLOR_GOOD);
  var ToDec := SendTextEntryGump(who,"Wpisz wartosc",TE_CANCEL_ENABLE);

  if(!ToDec)
    SendSysMessage(who,"Anulowano.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  

    
  var Mushrooms := FindObjTypeInContainer(who.backpack,0xF83);
  var MushCont  := Mushrooms.container;
  var WhoSkill  := GetBaseSkillBaseValue( who, skill );

  ToDec := CInt(CDbl(ToDec) * 10);

  var ForgetLimit := CInt(GetSkillConfigElem(skill).ForgetLimit);
  if((ForgetLimit * 10 <= GetBaseSkillBaseValue(who,Skill)) and (ForgetLimit * 10 > ToDec))
    SendSysMessage(who, "Ta umiejetnosc mozna zapomniec tylko do poziomu: " + ForgetLimit + "%.", FONT_NORMAL,COLOR_RED);
    return;
  endif;
        
  var CurDec    := WhoSkill;

  if(!ToDec)
    if(!YesNo(who,"Na pewno?"))
      SendSysMessage(who,"Anulowano.",FONT_NORMAL,COLOR_RED);
      return;
    endif
  endif

  if(!Mushrooms)
    SendSysMessage(who,"Nie masz grzybkow, dzieki ktorym mozesz zapominac.",FONT_NORMAL,COLOR_RED);
    return;
  endif
    
  if(ToDec >= WhoSkill)
    SendSysMessage(who, "Umiejetnosc mozesz co najwyzej zapomniec!",FONT_NORMAL,COLOR_BAD);
    return;
  endif

  var WhoSer  := who.serial,n;
  var bx      := who.x;
  var by      := who.y;
  var bz      := who.z;
  var bRlm    := who.realm;
  var KeepRun := 1;
  var EndDec  := 0;
  var DecPerTime, DecPhase, i;

  if(!StandardItemCheck(who, Mushrooms))
    return;
  endif

  if(IsBusy(who))
    return;
  endif
  
  while(CurDec >= 0 and who and KeepRun)

    who.hidden := 0;
    PrintTextAbove(who, "*zapomina*");

    for(n:=1;n<=DEC_SKILL_TIME_DIVIDE;n:=n+1)
      sleep(1);
      if(bx != who.x or by != who.y or bz != who.z or bRlm != who.realm)
        SendSysMessage(who, "Oddalil"+ggm(who,3)+" sie.", FONT_NORMAL, COLOR_RED);
        KeepRun := 0;
        break;
      endif
    endfor
     
    if(!who)
      who := FindPlayerBySerial(WhoSer);
    endif
  
    if(who.dead or !who or !IsLoggedIn(who) or !KeepRun)
      break;
    endif

    if(!MushRooms or MushRooms.container != MushCont)
      SendSysMessage(who, "Nie masz grzybkow, dzieki ktorym mozesz zapominac!",FONT_NORMAL,COLOR_RED);
      break;
    endif

    if(CurDec > GetBaseSkillBaseValue(who,Skill))
      CurDec := GetBaseSkillBaseValue(who,Skill);
    endif
    
    if(DecPhase     ==  2  and   CurDec % 100  == 0)
      DecPerTime  := 100;
    elseif(DecPhase >= 1   and   CurDec % 10   ==  0)
      DecPerTime  := 10;
      DecPhase     := 2;
    else 
      DecPerTime  := 1;
      DecPhase   := 1;
    endif

    for(i:=1;i<=DecPerTime;i:=i+1)
      if(CurDec <= ToDec or CurDec <= 0)
        EndDec := 1;
        break;
      endif
      if(!SubtractAmount(Mushrooms,1))
        SendSysMessage(who,"Skonczyly Ci sie grzybki.",FONT_NORMAL,COLOR_RED);
        EndDec := 1;
        break;
      endif
      CurDec := CurDec - 1;
    endfor

    SetBaseSkillBaseValue(who,Skill,CurDec);
      
    if(EndDec)
      break;
    endif

  endwhile
      
  ReleaseItem(Mushrooms);

  if(Skill == SKILLID_KENSAI or Skill == SKILLID_BARBARIAN)
    SetObjProperty(who, "Forgot_"+Skill, ReadGameClock());
  endif

  NoLongerBusy(who);
  SendSysMessage(who,"Zapomnial"+ggm(who,3)+" umiejetnosc do wartosci "+(CurDec*0.1)+"%.",FONT_NORMAL,COLOR_BLUE);

endfunction

//-----------------------------------------------------------------------------

function ChangeStatValue(Stat)

  if(who.frozen)
    SendSysMessage(who,"Jestes zamrozon"+ggm(who,6)+" i nie uda Ci sie tego zrobic!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(who.paralyzed)
    SendSysMessage(who,"Jestes sparalizowan"+ggm(who,6)+" i nie uda Ci sie tego zrobic!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(who.dead)
    SendSysMessage(who,"Jestes martw"+ggm(who,6)+"!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(!Stat or IsBusy(who))
    return;
  endif

  case ( Stat )
    1:  SendSysMessage(who, "Chcesz stac sie slabsz"+ggm(who,6)+"?", FONT_NORMAL, COLOR_GREEN);
    2:  SendSysMessage(who, "Chcesz stac sie mniej zreczn"+ggm(who,6)+"?", FONT_NORMAL, COLOR_GREEN);
    3:  SendSysMessage(who, "Chcesz stac sie mniej inteligentn"+ggm(who,6)+"?", FONT_NORMAL, COLOR_GREEN);
  endcase

  if(!YesNo(who,"Na pewno?"))
    SendSysMessage(who,"Anulowano.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  var WhoStat;

  case ( Stat )
      1: if (GetBaseStrengthBaseValue(who) <= 100)
           SendSysMessage(who, "Jestes juz wystarczajaco slab"+ggm(who,6)+".",FONT_NORMAL,COLOR_GOOD);
           return 0;
         endif
         WhoStat := GetBaseStrength(who);
      2: if (GetBaseDexterityBaseValue(who) <= 100)
           SendSysMessage(who, "Twa zrecznosc jest juz wystarczajaco niska.",FONT_NORMAL,COLOR_GOOD);
           return 0;
         endif
         WhoStat := GetBaseDexterity(who);
      3: if (GetBaseIntelligenceBaseValue(who) <= 100)
           SendSysMessage(who, "Twa inteligencja juz nizej zejsc nie moze.",FONT_NORMAL,COLOR_GOOD);
           return 0;
         endif
         WhoStat := GetBaseIntelligence(who);
         
      default: return;
  endcase
  
  var Mush := FindObjtypeInContainer( who.backpack, 0xf83 );

  if(!Mush)
    SendSysMessage(who, "Nie masz grzybkow, dzieki ktorym mozesz zapominac!",FONT_NORMAL,COLOR_BAD);
    return;
  endif

  if(!StandardItemCheck(who, Mush))
    return;
  endif

  var WhoSer := who.serial;
  
  if(Mush.amount < 20)
    SendSysMessage(who, "Masz za malo grzybkow aby kontynuowac.",FONT_NORMAL,COLOR_BAD);
    return;
  endif

  if(!SubtractAmount(Mush, 20))
    SendSysMessage(who, "Masz za malo grzybkow aby kontynuowac.",FONT_NORMAL,COLOR_BAD);
    return;
  endif

  PrintTextAbove(who, "*zapomina*");

  if(!MakeNoise(who, 0, 1000, 0, DEC_STAT_TIME_DIVIDE))
    return;
  endif
  
  if(!who)
    who := FindPlayerBySerial(WhoSer);
  endif

  if(who.dead or !who or !IsLoggedIn(who))
    return;
  endif
  
  case( Stat )
       1: var BStr := GetBaseStrengthBaseValue(who);
          SetBaseStrengthBaseValue(who, BStr - 1);
          PrintTextAbove(who, "*Staje sie slabsz"+ggm(who,6)+"*");
          
       2: var BDex := GetBaseDexterityBaseValue(who);
          SetBaseDexterityBaseValue(who, BDex - 1);
          PrintTextAbove(who, "*Staje sie mniej zreczn"+ggm(who,6)+"*");
          
       3: var BInt := GetBaseIntelligenceBaseValue(who);
          SetBaseIntelligenceBaseValue(who, BInt - 1);
          PrintTextAbove(who, "*Staje sie mniej inteligentn"+ggm(who,6)+"*");
  endcase
  
  NoLongerBusy(who);

endfunction

//-----------------------------------------------------------------------------

function ChangeBardSound(who,Nr)

  case(Nr)
    1:   SaveBardSound(who,S_SUCCESS_ID);
         SendSysMessage(who,"Bedziesz teraz gral"+ggm(who,4)+" normalnie.",FONT_NORMAL,COLOR_GREEN);
    2:   SaveBardSound(who,S_PEACE_ID);
         SendSysMessage(who,"Bedziesz teraz gral"+ggm(who,4)+" spokojnie.",FONT_NORMAL,COLOR_GREEN);
    3:   SaveBardSound(who,S_DRAMA_ID);
         SendSysMessage(who,"Bedziesz teraz gral"+ggm(who,4)+" dramatycznie.",FONT_NORMAL,COLOR_GREEN);
    4:   SaveBardSound(who,S_BATTLE_ID);
         SendSysMessage(who,"Bedziesz teraz gral"+ggm(who,4)+" walecznie.",FONT_NORMAL,COLOR_GREEN);
  endcase

endfunction

//-----------------------------------------------------------------------------

function MurdererDescription(Nr)

  case(Nr)
    1:   return "Masz wyrzuty sumienia.";
    2:   return "Masz nieczyste sumienie.";
    3:   return "Zadrecza cie sumienie.";
    4:   return "Ciemnosc ogarnia twa dusze.";
    default: return "";
  endcase

endfunction

//-----------------------------------------------------------------------------

function PvPStatusDescription(Nr)

  case(Nr)
    0:   return "Nie mozesz walczyc z innymi.";
    1:   return "Mozesz walczyc z innymi.";
    2:   return "Mozesz byc atakowany.";
    default: return "";
  endcase

endfunction

//-----------------------------------------------------------------------------

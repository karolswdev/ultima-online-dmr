

//-----------------------------------------------------------------------------

include "include/findcity";
include "include/log";
include "include/player/misc";
include "include/move";
include "include/gumpboxes";
include "include/clock";
include "include/cmds/cancmd";
include "include/checks/objchecks";
include "include/portals";
include "include/effects";
include "include/buildings";

//-----------------------------------------------------------------------------

const RETURN_MAX_DELAY    := 172800;

//-----------------------------------------------------------------------------

program ReturnRune(who, item)
  
  if(!StandardItemCheck(who, item, NO_COMBAT_CHECK))
    return;
  endif
  
  if(IsBusy(who))
    return;
  endif
  
  if(GetObjProperty(item, "x") == error)
    SendSysMessage(who, "To runo nie jest zaznaczone.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  var BuyTime := CInt(GetObjProperty(item, "BuyTime"));

  if((ReadGameClock() - BuyTime) > RETURN_MAX_DELAY)
    SendSysMessage(who, "To runo stracilo swoja moc. Czas minal." ,FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(!CanUseRune(who, item))
    return;
  endif

  SendSysMessage(who, "Wladca Portali probuje otworzyc portal do Ciebie...", FONT_NORMAL, COLOR_GREEN);
  
  // czekamy 12s z przerwami co 1.2s
  if(!MakeNoise(who, 0, 600, 0, 10, FX_HEAL_EFFECT))
    return;
  endif
  
  if(!CanUseRune(who, item))
    return;
  endif
  
  if(!MakeNoise(who, 0, 600, 0, 10, FX_HEAL_EFFECT))
    return;
  endif
  
  if(!CanUseRune(who, item))
    return;
  endif

  var Dx    := GetObjProperty(item, "x");
  var Dy    := GetObjProperty(item, "y");
  var Dz    := GetObjProperty(item, "z");
  var DRlm  := GetObjProperty(item, "Rlm");

  if(!DestroyItem(item))
    EraseObjProperty(item, "x");
    EraseObjProperty(item, "y");
    EraseObjProperty(item, "z");
    EraseObjProperty(item, "Rlm");
    SendSysMessage(who, "To runo nadaje sie tylko do smietnika." , FONT_NORMAL,COLOR_RED);
  endif
  
  Detach();
  NoLongerBusy(who);
  CreateGateTravel(who.x, who.y, who.z, who.realm, Dx, Dy, Dz, DRlm, CGTF_NO_HOSTILES);

endprogram

//-----------------------------------------------------------------------------

function CanUseRune(who, item)

  if(who.dead)
    SendSysMessage(who, "Jestes martw"+ggm(who,6)+"!", FONT_NORMAL, COLOR_RED);
    return 0;
  endif

  if(!IsLoggedIn(who))
    return 0;
  endif
  
  if(who.frozen or who.paralyzed)
   SendSysMessage(who, "Czar prysl...", FONT_NORMAL, COLOR_RED);
   return 0;
  endif
  
  if(who.hidden)
    who.hidden := 0;
  endif
  
  var Dx    := GetObjProperty(item, "x");
  var Dy    := GetObjProperty(item, "y");
  var Dz    := GetObjProperty(item, "z");
  var DRlm  := GetObjProperty(item, "Rlm");

  if(NoTeleportZone(Dx, Dy, Dz, DRlm) or NoTeleportZone(who.x,who.y,who.z,who.realm))
    SendSysMessage(who,"W tym miejscu magia nie dziala." ,FONT_NORMAL,COLOR_RED);
    return 0;
  endif

  if(coordist(who.x, who.y, Dx, Dy) < 3 and who.realm == DRlm)
    SendSysMessage(who, "Znajdujesz sie w miejscu docelowym..." ,FONT_NORMAL,COLOR_RED);
    return 0;
  endif
  
  if(IsWaterInRange(who.x,who.y,who.z,who.realm, 10))
    SendSysMessage(who, "Nie mozesz przywolac portalu, jestes zbyt blisko wody.",FONT_NORMAL,COLOR_RED);
    return 0;
  endif

  /*
  __NONPVP__ DELETED
  if(IsInNonPvPArea(Dx,Dy,DRlm) != IsInNonPvPArea(who.x,who.y,who.realm))
    SendSysMessage(who, "Nie mozesz przywolac portalu z tej krainy." ,FONT_NORMAL,COLOR_RED);
    return 0;
  endif
  */

  return 1;
  
endfunction

//-----------------------------------------------------------------------------

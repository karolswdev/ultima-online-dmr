//-----------------------------------------------------------------------------

include ":magic:magic";
include "include/gumps";
include "include/portals";

include ":jail:common";

//-----------------------------------------------------------------------------

const SPELL_ID   := 90;

//-----------------------------------------------------------------------------

program CastSummonPlayer( nSpellParms )

  SpellCast(nSpellParms, SPELL_ID, TRGT_NONE);

endprogram

//-----------------------------------------------------------------------------

function SpellAction(Caster, Cast)

  __ParamUnused(Cast);

  var CasterLoc := array(Caster.x, Caster.y, Caster.z, Caster.realm);
  var CastOn := SelectPlayer( caster );

  if(CasterLoc[1] != Caster.x or CasterLoc[2] != Caster.y or CasterLoc[3] != Caster.z or CasterLoc[4] != Caster.realm)
    Fizzle(Caster, "Oddalil"+ggm(Caster,3)+" sie.");
  else
    OpenGate( caster, CastOn );
    GainSpellPoints(Caster, CastOn, MAGICGAIN_NORMAL); 
  endif
  
endfunction

//-----------------------------------------------------------------------------

function SelectPlayer( who )

  var Arr := GetOnlinePlayersList(who);

  if(!Arr.size())
    Fizzle(who,"Nie ma kogo przywolac.");
    exit;
  endif

  GInit(50,50);
  GPage();
  GResPic(0,0,BKGND_BLACKSLAB,280,400);
  GResPic(16,18,BKGND_GOLDTRIM,246,366);
  GTextLine(65,38 ,590,"Kogo przyciagnac?");
  GAddPageData(237,29,2650,2651,237,354,2646,2647);

  var i := 1;

  foreach Memb in Arr
    GCheckNextPage(15);
    GTextLine(55,65+(GNPCount() * 20),560,ClStr(Memb));
    GButton(32, 65+(GNPCount() * 20),2714,2715,i);
    i := i + 1;
  endforeach

  var Res := GSend(who)[0];

  if(Res)
    return Arr[Res];
  endif
  return 0;

endfunction

//-----------------------------------------------------------------------------

function GetOnlinePlayersList(who)

  var Arr := array;

  foreach plr in EnumerateOnlineCharacters()
    if(!plr.cmdlevel and plr != who and !IsStaff(plr))
      if (!GetObjProperty( plr, "private"))
        Arr.append( plr.name );
      endif
    endif
  endforeach

  Arr.sort();

  return Arr;

endfunction

//-----------------------------------------------------------------------------

function FindCastOn( cast_on )

  foreach plr in EnumerateOnlineCharacters()
    if (plr.name == cast_on )
      return plr;
    endif
  endforeach
  return 0;

endfunction

//-----------------------------------------------------------------------------

function OpenGate( caster, caston )

  if(!CastOn or Caster.dead)
    Fizzle(Caster,"Anulowano.");
    return;
  endif
  
  CastOn := FindCastOn( CastOn );

  if(CInt(GetObjProperty(Caster, "DruidGateDelay")) > ReadGameClock())
    Fizzle( Caster, "Mozesz przywolac portal raz na 3 dni.");
    return;
  endif

  if(IsJailed(CastOn))
    Fizzle( caster, "Twoj portal zawiodl...");
    return;
  elseif(!CantOpenGateAt(Caster, CastOn))
    Fizzle( caster, "Twoj portal zawiodl...");
    return;
    /*
  __NONPVP__ DELETED
  elseif(IsInNonPvPArea(caster.x,caster.y,caster.realm) and !IsInNonPvPArea(caston.x,caston.y,caston.realm))
    Fizzle(Caster,"Nie mozna stawiac portali miedzy strefami");
    return;*/
  endif

  SetObjProperty(Caster, "DruidGateDelay", ReadGameClock() + 1800);

  var Cx          := Caster.x;
  var Cy          := Caster.y;
  var Cz          := Caster.z;
  var CRlm        := Caster.realm;
  var Dx          := CastOn.x;
  var Dy          := CastOn.y;
  var Dz          := CastOn.z;
  var DRlm        := CastOn.realm;

  PlaySoundEffect(caster, SFX_SPELL_GATE_TRAVEL);
  var PreGate       := CreatePortal(UOBJ_BLUEGATE, Cx,Cy,Cz, CRlm, 0);
  PreGate.color     := 1435;
  PreGate.facing    := 1;
  Pregate.name      := "Druidyczny portal";

  var Gate          := 0;
  
  if(!CastOn.hidden or IsFriend(CastOn, Caster))
    PlaySoundEffect(CastOn, SFX_SPELL_GATE_TRAVEL);
    Gate              := CreatePortal(UOBJ_BLUEGATE,Dx,Dy,Dz, DRlm, 0);
    Gate.color        := 1435;
    Gate.facing       := 1;
    Gate.name         := "Druidyczny portal";
  endif
  
  if(Gate)
    ShowPortal(array(PreGate,Gate));
    
    SetObjProperty(Gate, "DruPortal", Caster.serial);
    SetObjProperty(Gate, "DestX", Cx);
    SetObjProperty(Gate, "DestY", Cy);
    SetObjProperty(Gate, "DestZ", Cz);
    SetObjProperty(Gate, "DestRlm", CRlm);
  else
    ShowPortal(PreGate);
  endif

  SetObjProperty(PreGate, "NoTele", 1);

  Detach();
  HidePortal(PreGate);

  var i;
  for(i:=1;i<=10;i:=i+1)
    ShowPortal(PreGate);
    HidePortal(PreGate);
  endfor
    
  DestroyItem (PreGate);
  if(Gate)
    HidePortal(Gate);
    DestroyItem(Gate);
  endif

endfunction

//-----------------------------------------------------------------------------

function CantOpenGateAt(who, Loc)

  if(NoTeleportZone(Loc.x, Loc.y, Loc.z, Loc.realm, TCF_NOCHECK_BLD | TCF_NOCHECK_DUNG | TCF_CHECK_CHAMP | TCF_NOCHECK_MULTI))
    return 0;
  endif

  if(NoTeleportZone(who.x, who.y, who.z, who.realm))
    return 0;
  endif

  if(IsWaterInRange(Loc.x,Loc.y,Loc.z,Loc.Realm, 10))
    return 0;
  elseif(IsWaterInRange(who.x,who.y,who.z,who.realm, 10))
    return 0;
  endif

  return 1;

endfunction

//-----------------------------------------------------------------------------

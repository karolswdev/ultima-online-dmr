//-----------------------------------------------------------------------------

include "include/skills/std";
include "include/calc";

include ":boat:plankutil";
include ":quests:quest_ctrl";
include ":housing:housing";

//-----------------------------------------------------------------------------

const DOOR_PICK_DIFF  := 101;
const DOOR_LOCK_DELAY := 180; // 3 minut delaya na zamkniecie drzwi.

//-----------------------------------------------------------------------------

var ItemCfg := ReadConfigFile(ITEMS_CFG);

//-----------------------------------------------------------------------------

program Lockpicking(who, lockpick)

  if(IsBusy(who))
    return;
  endif
  
  if(Malloc(who))
	return;
  endif

  if(!StandardItemCheck(who,lockpick))
    return;
  endif

  SendSysMessage(who, "Wybierz przedmiot.",FONT_NORMAL,COLOR_GREEN);
  var Item := ObjTarget(who);

  if(!StandardItemCheck(who,Item,NO_MOVABLE_CHECK))
    return;
  endif
  
  var MaxDist := 1;
  if (IsPlank(Item)) //Poniewaz bedziemy udostepniac mozliwosc rowniez wlamywania sie do lodzi
      MaxDist := 3;            //to nalezy zwiekszyc max dist - ciezko sie wlamac do lodzi z 1 kratki.
  endif

  if(Distance(Item, who) > MaxDist )  
    SendSysMessage(who, "Jestes za daleko.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(!Item.locked)
    SendSysMessage(who, "To nie jest zamkniete.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  SetBusyTime(who,GetSkillDelay(SKILLID_LOCKPICKING));

  var Diff := 0;

  if(Item.isa(POLCLASS_DOOR) or IsPlank(Item))
    if(GetBaseSkill(who,SKILLID_HIDING) < 100 or GetBaseSkill(who,SKILLID_STEALTH) < 100 or GetBaseSkill(who,SKILLID_LOCKPICKING) < 100 or GetBaseSkill(who,SKILLID_STEALING) < 100)
      SendSysMessage(who,"Twa wiedza jest za mala by moc sie wlamac tutaj.",FONT_NORMAL,COLOR_RED);
      return;
    endif
    Diff := DOOR_PICK_DIFF;
    var House := Item.multi;
    if(House)
      var DeedObj := GetDeedObjTypeByHouseType(House.objtype);
      Diff := Diff + CInt(ItemCfg[DeedObj].NumSecure / 3);
    endif
  else
    Diff := CInt(ItemCfg[Item.objtype].LockPickDiff);
    if(!Diff)
      Diff := CInt(GetObjProperty(Item, PROP_LOCKPICKDIFF));
    endif
  endif
  
  if((!Item.movable and !Item.multi and !GetObjProperty(Item, QTPROP_ITEMCRTIME) and !IsPlank(Item) and !Item.isa(POLCLASS_DOOR)) or !Diff or GetObjProperty(Item, PROP_NO_LOCKPICK))
    SendSysMessage(who, "Nie mozesz tego otworzyc wytrychami.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  //proba wlamania sie zawsze bedzie sie konczyla sie krimem - jesli jest ktos w poblizu
  CheckLockpickCriminal(who); 

  Diff := Diff - (CheckItemQuality(lockpick) / 5);

  var Num := 5;

  if(Item.multi)
    var DeedObj := GetDeedObjTypeByHouseType(Item.multi.objtype);
    Num := Num + CalcLinear(ItemCfg[DeedObj].VendorSellsFor, 25, 2000000); // doda 0-40 wg. ceny domu
  endif

  SendSysMessage(who, "Probujesz sie wlamac...", FONT_NORMAL, COLOR_GREEN);

  if(!MakeNoise(who, 0, 1500, 0, Num))
    return;
  endif

  if(CheckSkill(who, SKILLID_LOCKPICKING, diff,CInt(diff/2)))
    PrintTextAbovePrivate(Item,"*otwarte!*",who,FONT_NORMAL,COLOR_GREEN);
    UnlockDoor(who, Item);
    SendQuestEvent(who, EVQID_SKILL, SKILLID_LOCKPICKING, Item);
  else
    Sendsysmessage(who,"Nie udalo Ci sie otworzyc tego zamka.",FONT_NORMAL,COLOR_RED);
    if(RandomInt(Diff+10) > GetEffectiveSkill(who,SKILLID_LOCKPICKING))
      PlaySoundEffect(Item,0xef);
      SendSysMessage(who,"Wytrychy sie polamaly!",FONT_NORMAL,COLOR_RED);
      SubtractAmount(lockpick,1);
    endif
  endif
  
endprogram

//-----------------------------------------------------------------------------

function CheckLockpickCriminal(who)

  foreach Obj in ListObjectsInBox(who.x - 8, who.y - 8, -127, who.x + 8, who.y + 8, 127, who.realm)
    if(IsPlayer(Obj) and !Obj.dead and !Obj.concealed and !Obj.cmdlevel and Obj != who)
      SetCriminal(who, 1, "CheckLockpickCriminal", "Wlamywal"+ggm(who,4)+" sie do domu, widzial"+ggm(Obj,4)+" "+ggm(who,16)+" "+Obj.name+" ["+Obj.serial+"] ["+Obj.x+" "+Obj.y+" "+Obj.z+" "+Obj.realm+"], Hid: "+Obj.hidden+".");
      return;
    endif
  endforeach

endfunction

//-----------------------------------------------------------------------------

function UnlockDoor(who, Door)

  Door.locked := 0;

  if(Door.isa(POLCLASS_DOOR))
    SetObjProperty(who,"Lockpicked",{CInt(Door.multi.serial),ReadGameClock()});
    SetObjProperty(Door, "#LockDelay", ReadGameClock() + DOOR_LOCK_DELAY);

    if(Door.multi)
      var Doors := FindHouseDoors(Door.multi);

      foreach Item in Doors
        Item.locked := 0;
        SetObjProperty(Item, "#LockDelay", ReadGameClock() + DOOR_LOCK_DELAY);
      endforeach
    endif
  endif
   
endfunction

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

use uo;

//-----------------------------------------------------------------------------

include "include/key";
include "include/gumpboxes";
include ":banking:common";
include "include/escrow";
include "include/clock";
include "include/tempmods";
include "include/checks/objchecks";
include "include/cmds/cancmd";
include "include/move";
include "include/std";
include "include/speech";

include ":fences:fences";
include ":carpentry:deeds";
include ":carpets:rolls";
include ":housing:housing";
include ":housing:payments";

//-----------------------------------------------------------------------------

const SPAGE_COUNT                    := 8;
const MAX_HOUSE_FRIENDS              := 20;
const MAX_HOUSE_COOWNERS             := 5;
const MAX_HOUSE_NAME_WORDS           := 3;
const MAX_HOUSE_NAME_WORD_LETTERS    := 10;

const MSG_TIME                       := 3600;
const MSG_NUM                        := 3;

//-----------------------------------------------------------------------------

var keeprun := 1;
var gHouse,gOwner,who,sign;
var IsHOwner := 0,IsCoOwner := 0,IsHFriend := 0;
var Lines;
var ProperLocHouse;

//-----------------------------------------------------------------------------

program HouseSign(who1,sign1)
 
  SetLogRunCycles(LST_CMD);

  who   := who1;sign := sign1;
  gHouse := GetHouse(sign);
  gOwner := GetHouseOwner(gHouse);
  ProperLocHouse := 0;

  if(!gHouse)
    DestroyItem(sign);
    SendSysMessage(who,"Dom zdaje sie nie istniec.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  if(!gOwner)
    gOwner := SetNewOwnerOfHouse(gHouse);

    if(!gOwner)
      DestroyHouse(gHouse, 1);
      SendSysMessage(who,"Ten dom rozlecial sie na twoich oczach!",FONT_NORMAL,COLOR_RED);
      return;
    endif
  endif
  
  if(IsHouseOwner(who,gHouse))
    IsHOwner := 1;
  elseif(IsHouseCoOwner(who,gHouse))
    IsCoOwner := 1;
  elseif(IsHouseFriend(who,gHouse))
    IsHFriend := 1;
  endif

  if(IsHOwner or IsCoOwner or IsHFriend or IsStaff(who))
    ProperLocHouse := CanBuildHouseAt(GetDeedObjTypeByHouseType(gHouse.objtype), gHouse, gHouse.realm, gHouse);
    if(!ProperLocHouse)
      SendSysMessage(who, "Twoj dom stoi w nieprawidlowym miejscu.", FONT_NORMAL, COLOR_RED);
      SendSysMessage(who, ProperLocHouse.errortext, FONT_NORMAL, COLOR_RED);
    elseif(IsHouseAccepted(gHouse) < 0)
      SendSysMessage(who, "Twoj dom stoi w nieprawidlowym miejscu.", FONT_NORMAL, COLOR_ORANGE);
      SendSysMessage(who, "Mimo to zostal tymczasowo zaakceptowany, abys mogl go samodzielnie przestawic.", FONT_NORMAL, COLOR_WHITE);
      SendSysMessage(who, "Pozostaly czas: "+RealTime(GetObjProperty(gHouse, "TerrainAccepted") - ReadGameClock()), FONT_NORMAL, COLOR_WHITE);
    endif
  endif
  
  BuildMainHouseSignGump();

  var res;
  
  while(keeprun and sign and who and gOwner and gHouse)

    res := GSend(who);
    if(!res.butid)
      keeprun := 0;
    endif

    case(res.butid)
      11:    KickFromHouse(who,sign);
      12:    AcceptTerrain(who);
      13:    AcceptTerrainTemporary(who);
      21:    ShowCoOwnersList();
             continue;
      22:    ShowFriendsList();
             continue;
      31:    ChangeHouseName();
      32:    ChangeOwner();
      33:    DestroyHouseAndGetDeed();
      34:    SetPrivate();
      35:    ChangeSignGraphic();
             continue;
      36:    SetPublic(gHouse,who);
      37:    ChangeLocks();
      9999:  keeprun := 0;
	  9911:  MoveObjectToLocation(who, gHouse.x, gHouse.y, gHouse.z, gHouse.realm); var res := SendHousingTool(who, gHouse); SendSysMessage(who, res.errortext); break;
      190:   AddCoOwner();
      191:   EraseCoOwnerList();
      290:   AddFriend();
      291:   EraseFriendList();
      40:    ShowMsgs();
             continue;
      41:    if(CanMsg())
               BuildAddMsg();
             endif
             ShowMsgs();
             continue;
      42:    EraseMsgs();
             ShowMsgs();
             continue;

      51:    MakeHousePayment(who, gHouse);

      52:    if(CanUseCmd(who, "housesign decrease"))
               DecreaseHousePayment(who, gHouse);
             endif
    endcase
    
    res := res.butid;
    
    if(res > 100 and res <= 100 + MAX_HOUSE_COOWNERS)
      EraseCoOwner(res-100);
    elseif(res > 200 and res <= 200 + MAX_HOUSE_FRIENDS)
      EraseFriend(res-200);
    elseif((res >= UOBJ_SHOP_SIGN_START and res <= UOBJ_SHOP_SIGN_END) or (res >= UOBJ_GUILD_SIGN_START and res <= UOBJ_GUILD_SIGN_END))
      ChangeGraphic(res);
    elseif(res >= 300 and res <= 3000)
      EraseMsg(res-300);
      ShowMsgs();
      continue;
    elseif(res >= 30000 and res <= 300000)
      if(ShowSignMsg(res-30000))
        continue;
      endif
    endif
    
    BuildMainHouseSignGump();

  endwhile
    
endprogram

//-----------------------------------------------------------------------------

function BuildMainHouseSignGump()

  var OwnerName;

  if(!gOwner or (gOwner.cmdlevel and !who.cmdlevel))
    OwnerName := "nieznany";
  else
    OwnerName := GetRealName(gOwner);
  endif

  if(IsHOwner or IsGm(who) or IsCoOwner or IsHFriend)
    StandSignLayout();
  else
    ShowMsgs();
    return;
  endif

  GGumpPic(210,40,100);
  FillName();

  if(!IsHOwner and !IsGm(who) and !IsCoOwner and !IsHFriend)
    return;
  endif

  var IsHouseLockedDown := !ProperLocHouse and !IsGM(who);
  
  GButtonPage(130,150,2714,2715,1);
  GTextLine(150,150,640,"Info");

  if(!IsHouseLockedDown)
    GButtonPage(190,150,2714,2715,2);
    GTextLine(210,150,640,"Przyjaciele");

    GButtonPage(300,150,2714,2715,3);
    GTextLine(320,150,640,"Opcje");
  endif

  if(gHouse.custom)
	GButton(370,150,2714,2715,9911);
	GTextLine(390,150,640,"Buduj");
  endif

  GPage();

  if(IsHouseLockedDown)
    GTextLine(140,190,1209,"Twoj dom stoi w nieprawidlowym miejscu.");
    GTextLine(140,210,1209,ProperLocHouse.errortext);
    
    GButton(130,270,2714,2715,33);
    GTextLine(160,270,550,"Zniszcz dom i odzyskaj deeda");
  else
    GTextLine(180,190,550,"Wlasciciel");
    GTextLine(250,190,570,OwnerName);
    
    if(IsStaff(who) and GetObjProperty(gHouse, "GMName"))
      GTextLine(180,210,550,"Postawil");
      GTextLine(250,210,1410,GetObjProperty(gHouse, "GMAccName")+"/"+GetObjProperty(gHouse, "GMName"));
    endif

    GTextLine(150,240,550,"Zabezpieczone pojemniki");
    GTextLine(330,240,570,GetSecured(gHouse));

    GTextLine(150,260,550,"Zablokowane przedmioty");
    GTextLine(330,260,570,GetLockedDown(gHouse));
    
    GButton(130,300,2714,2715,11);
    GTextLine(160,300,550,"Wyrzuc kogos z domu");

    if(IsStaff(who) and IsHouseAccepted(gHouse) > 0)
      GTextLine(160,320,1310,"Teren zaakceptowany");
    elseif(IsHouseAccepted(gHouse) < 0)
      GTextLine(160,320,1310,"Teren tymczasowo zaakceptowany.");
    endif

    GButton(330,300,2714,2715,40);
    GTextLine(350,300,640,"Wiadomosci");
  endif

  GPage();

  if(!IsHouseLockedDown)
    GButton(130,190,2714,2715,21);
    GTextLine(160,190,550,"Zabacz liste Co - wlascicieli");

    GButton(130,220,2714,2715,22);
    GTextLine(160,220,550,"Zabacz liste Przyjaciol");

    if(IsPublicHouse(gHouse))
      GTextLine(160,280,550,"Liczba Kupcow : ");
      GTextLine(285,280,570,len(GetHouseVendors(gHouse)));
    endif
  endif
    
  GPage();

  if(!IsHouseLockedDown)
    GButton(130,190,2714,2715,31);
    GTextLine(160,190,550,"Zmien nazwe domu");

    GButton(130,220,2714,2715,32);
    GTextLine(160,220,550,"Przekaz dom");

    GButton(130,250,2714,2715,33);
    GTextLine(160,250,550,"Zniszcz dom i odzyskaj deeda");

    if(IsPublicHouse(gHouse))
      GButton(130,280,2714,2715,34);
      GTextLine(160,280,550,"Zadeklaruj dom prywatnym");

      GButton(130,310,2714,2715,35);
      GTextLine(160,310,550,"Zmien wyglad znaku");
    else
      GButton(130,280,2714,2715,36);
      GTextLine(160,280,550,"Zadeklaruj dom publicznym");

      GButton(130,310,2714,2715,37);
      GTextLine(160,310,550,"Zmien zamek w drzwiach");
    endif
    
    if(IsAdmin(who))
      GButton(300,190,2714,2715,12);
      if(GetObjProperty(gHouse, "TerrainAccepted"))
        GTextLine(320,190,1310,"Wylacz akceptacje");
      else
        GTextLine(320,190,590,"Akceptuj teren");
        
        GButton(300,220,2714,2715,13);
        GTextLine(320,220,590,"Tymczasowo akceptuj");
      endif
    endif

  endif

  GPage();

  var PaymentTime := GetHousePaymentTime(gHouse);

  GTextLine(140, 190, 550, "Dom oplacony do:");
  GTextLine(140, 210, 590, GetDescDate(PaymentTime));

  var Periods := GetNotPayedPeriods(gHouse);

  if(Periods >= HOUSEPAY_MAX_PERIODS-2)
    GTextLine(140, 230, 1209, "Dom nie jest oplacony!!");
    GTextLine(140, 250, 1209, "Niedlugo zostanie zniszczony!");
  elseif(Periods >= HOUSEPAY_LOCK_PERIODS)
    GTextLine(140, 230, 1209, "Dom nie jest oplacony! Zostal zamkniety!");
  elseif(Periods > 0)
    GTextLine(140, 230, 1126, "Dom zalega z platnosciami!");
    GTextLine(140, 250, 1126, "Niedlugo zostanie zamkniety.");
  endif

  if(CanPayForHouse(gHouse))
    
    GButton(115,277, 0xFBA, 0xFBB, 51);
    GTextLine(150,280, 550, "Oplac nastepny okres do:");

    if(CanUseCmd(who, "housesign decrease"))
      GButton(355,277, 0xFBA, 0xFBB, 52);
      GTextLine(390,280, 550, "Obniz");
    endif

    GTextLine(150,300, 590, GetDescDate(PaymentTime + HOUSEPAY_PERIOD_TIME) + " ("+GameTime(HOUSEPAY_PERIOD_TIME)+")");
    GTextLine(150,320, 550, "Koszt:");
    GTextLine(190,320, 1309, GetHousePayment(gHouse)+" gp");
  endif

endfunction

//-----------------------------------------------------------------------------

function GetSecured(gHouse)

  return GetSecureNum(gHouse) + "/" + GetSecureMaxNum(gHouse);

endfunction

//-----------------------------------------------------------------------------

function GetLockedDown(gHouse)

  return GetLockDownsNum(gHouse) + "/" + GetLockDownsMaxNum(gHouse);

endfunction

//-----------------------------------------------------------------------------

function StandSignLayout(More := 0)

  GInit();
  GMode(GMODE_NORMAL, GSMODE_PACKET);
  GPage();
  GResPic(90,90,2620,380,280 + More);
  GResPic(98,95,3600,367,265 + More);

endfunction

//-----------------------------------------------------------------------------

function StdMultiSignLayout(More := 0)

  GInit();
  GMode(GMODE_MULTIHD, GSMODE_PACKET);
  GResPic(90,90,2620,380,280 + More);
  GResPic(98,95,3600,367,265 + More);

endfunction

//-----------------------------------------------------------------------------

function FillName()

  var Name := SplitWords(GetHouseName(gHouse));

  if(Name[3])
    GTextLine(230,55,650, Name[1]);
    GTextLine(230,79,650, Name[2]);
    GTextLine(230,103,650,Name[3]);
  elseif(Name[2])
    GTextLine(230,69,650,Name[1]);
    GTextLine(230,95,650,Name[2]);
  elseif(Name[1])
    GTextLine(230,79,650,Name[1]);
  endif
  
endfunction

//-----------------------------------------------------------------------------

function ShowCoOwnersList()

  StandSignLayout();
  GTextLine(130,115,550,"Co-Wlasciciele");

  if(IsHOwner or IsAdmin(who))
    GButton(125,320,2714,2715,190);
    GTextLine(150,320,550,"Dodaj Co-Wlasciciela");

    GButton(320,320,2714,2715,191);
    GTextLine(345,320,550,"Wyczysc liste");

    GTextLine(400,115,550,"Usun");
  endif

  var CoOwners := CheckCharList(GetHouseCoOwners(gHouse));
  SetHouseCoOwners(gHouse,CoOwners);
  ShowList(CoOwners,100);

endfunction

//-----------------------------------------------------------------------------

function ShowFriendsList()

  StandSignLayout();

  GTextLine(130,115,550,"Przyjaciele");

  if(IsHOwner or IsCoOwner or IsAdmin(who))
    GButton(125,320,2714,2715,290);
    GTextLine(150,320,550,"Dodaj Przyjaciela");

    GButton(320,320,2714,2715,291);
    GTextLine(345,320,550,"Wyczysc liste");

    GTextLine(400,115,550,"Usun");
  endif

  var Friends := CheckCharList(GetHouseFriends(gHouse));
  SetHouseFriends(gHouse,Friends);
  ShowList(Friends,200);

endfunction

//-----------------------------------------------------------------------------

function ShowList(Arr,Num)

  var i := 1;
  GButton(280,115,2714,2715,9998);
  GTextLine(305,115,550,"Wroc");
  GAddPageData(445,97,2650,2651,445,342,2648,2647);

  foreach Char in Arr
    Char := FindPlayerBySerial(Char);
    if(!Char)
      continue;
    endif
    GCheckNextPage(SPAGE_COUNT);
    GTextLine(130,140+(GNPCount()*20),560,i);
    GTextLine(155,140+(GNPCount()*20),560,GetRealName(Char));
    if(IsHOwner or IsAdmin(who))
      GButton(410,140+(GNPCount()*20),2714,2715,Num+i);
    endif
    i := i + 1;
  endforeach
  
endfunction

//-----------------------------------------------------------------------------

function AddCoOwner()

  var NewCoOwner := ObjTarget(who);
  if(!StandardMobCheck(who,NewCoOwner))
    return;
  endif
  
  if(!IsPlayer(NewCoOwner))
    SendSysMessage(who,"To stworzenie nie moze zostac Co-wlascicielem!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(NewCoOwner == who)
    return;
  endif

  if(NewCoOwner == gOwner)
    SendSysMessage(who,"Ta osoba jest wlascicielem tego domu!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(IsHouseCoOwner(NewCoOwner,gHouse))
    SendSysMessage(who,"Ta osoba jest juz Co-wlascicielem.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  var CoOwners := GetHouseCoOwners(gHouse);
  if(len(CoOwners) >= MAX_HOUSE_COOWNERS)
    SendSysMessage(who,"Nie mozesz dodac wiecej Co-wlascicieli.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  CoOwners.append(NewCoOwner.serial);
  SetHouseCoOwners(gHouse,CoOwners);
  SendSysMessage(who,"Dodano nowego Co-wlasciciela "+NewCoOwner.name+".",FONT_NORMAL,COLOR_GREEN);

endfunction

//-----------------------------------------------------------------------------

function AddFriend()

  var NewFriend := ObjTarget(who);
  if(!StandardMobCheck(who,NewFriend))
    return;
  endif

  if(!IsPlayer(NewFriend))
    SendSysMessage(who,"To stworzenie nie moze zostac przyjacielem domu!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(NewFriend == who)
    return;
  endif

  if(NewFriend == gOwner)
    SendSysMessage(who,"Ta osoba jest wlascicielem tego domu!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(IsHouseFriend(NewFriend,gHouse))
    SendSysMessage(who,"Ta osoba jest juz przyjacielem domu.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  var Friends := GetHouseFriends(gHouse);
  if(len(Friends) >= MAX_HOUSE_FRIENDS)
    SendSysMessage(who,"Ta osoba jest juz przyjacielem domu.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  Friends.append(NewFriend.serial);
  SetHouseFriends(gHouse,Friends);
  SendSysMessage(who,"Dodano nowego przyjaciela "+NewFriend.name+".",FONT_NORMAL,COLOR_GREEN);

endfunction

//-----------------------------------------------------------------------------

function EraseCoOwner(Nr)

  var CoOwners := GetHouseCoOwners(gHouse);
  var CoOwner  := FindPlayerBySerial(CInt(CoOwners[Nr]));
  if(CoOwner)
    CoOwners.erase(Nr);
    SetHouseCoOwners(gHouse,CoOwners);
    SendSysMessage(who,"Skasowano Co-wlasciciela "+GetRealName(CoOwner)+".",FONT_NORMAL,COLOR_GREEN);
  else
    SendSysMessage(who,"Osoba nie istnieje.",FONT_NORMAL,COLOR_RED);
  endif

endfunction

//-----------------------------------------------------------------------------

function EraseFriend(Nr)

  var Friends := GetHouseFriends(gHouse);
  var Friend  := FindPlayerBySerial(CInt(Friends[Nr]));
  if(Friend)
    Friends.erase(Nr);
    SetHouseFriends(gHouse,Friends);
    SendSysMessage(who,"Skasowano przyjaciela domu "+GetRealName(Friend)+".",FONT_NORMAL,COLOR_GREEN);
  else
    SendSysMessage(who,"Osoba nie istnieje.",FONT_NORMAL,COLOR_RED);
  endif

endfunction

//-----------------------------------------------------------------------------

function EraseCoOwnerList()

  if(!YesNo(who,"Skasowac?"))
    SendSysMessage(who,"Anulowano.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  SetHouseCoOwners(gHouse,{});
  SendSysMessage(who,"Skasowano liste Co-wlascicieli.",FONT_NORMAL,COLOR_GREEN);
  
endfunction

//-----------------------------------------------------------------------------

function EraseFriendList()

  if(!YesNo(who,"Skasowac?"))
    SendSysMessage(who,"Anulowano.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  SetHouseFriends(gHouse,{});
  SendSysMessage(who,"Skasowano liste przyjaciol.",FONT_NORMAL,COLOR_GREEN);

endfunction

//-----------------------------------------------------------------------------

function ChangeHouseName()

  if(!IsHOwner and !IsCoOwner and !IsAdmin(who))
    SendSysMessage(who,"Nie mozesz tego zrobic.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  var NewName := SendTextEntryGump(who,"Wpisz nowa nazwe domu",TE_CANCEL_ENABLE);
  if(!NewName)
    SendSysMessage(who,"Anulowano.");
  endif
  
  NewName := SplitWords(NewName);
  if(Len(NewName) > MAX_HOUSE_NAME_WORDS)
    SendSysMessage(who,"Nazwa za dluga.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  foreach Word in NewName
    if(len(Word) > MAX_HOUSE_NAME_WORD_LETTERS)
      SendSysMessage(who,"Nazwa za dluga.",FONT_NORMAL,COLOR_RED);
      return;
    endif
    Word[1] := upper(Word[1]);
    Word[2,len(word)] := lower(Word[2,len(word)]);
  endforeach
  
  SetHouseName(gHouse,CombineWords(NewName));

endfunction

//-----------------------------------------------------------------------------

function ChangeLocks()

  if((!IsHOwner and !IsCoOwner) and !IsAdmin(who))
    SendSysMessage(who,"Nie mozesz tego zrobic.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  SendSysMessage(who,"Wybierz klucz.",FONT_NORMAL,COLOR_GREEN);
  var NewKey := ObjTarget(who);
  if(!StandardItemCheck(who,NewKey))
    ReleaseItem(NewKey);
    NoLongerBusy(who);
    return;
  endif

  if(GetItemLockID(NewKey))
    SendSysMessage(who, "Ten klucz zostal juz wyrobiony.", FONT_NORMAL, COLOR_RED);
    return;
  endif

  if((NewKey.objtype >= UOBJ_KEYSTART) and (NewKey.objtype <= UOBJ_KEYEND) and (NewKey.objtype != UOBJ_KEYRING))
    var LockID := AllocLockID();
    SetItemLockID(NewKey,LockID);
    foreach Door in FindHouseDoors(gHouse)
      SetItemLockID(Door,LockID);
    endforeach
    SendSysMessage(who,"Zamki zostaly zmienione.",FONT_NORMAL,COLOR_GREEN);
  else
    SendSysMessage(who,"To nie jest klucz.",FONT_NORMAL,COLOR_RED);
  endif

  ReleaseItem(NewKey);
  NoLongerBusy(who);

endfunction

//-----------------------------------------------------------------------------

function ChangeOwner()

  if(!IsHOwner and !IsAdmin(who))
    SendSysMessage(who,"Nie mozesz tego zrobic.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  if(!IfHouseContainsBoundItem( who, ghouse ))
    return;
  endif

  SendSysMessage(who,"Wybierz nowego wlasciciela.",FONT_NORMAL,COLOR_GREEN);
  var NewOwner := ObjTarget(who);
  if(!StandardMobCheck(who,NewOwner))
    return;
  endif
  if(!IsPlayer(NewOwner))
    SendSysMessage(who,"To stworzenie nie moze by wlascicielem domu!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(who.cmdlevel and GetObjProperty(gHouse,PROP_CREATED) and !NewOwner.cmdlevel)
    SendSysMessage(who, "Ten dom nie moze nalezec do smiertelnika.", FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  if(NewOwner == who)
    return;
  endif

  var List := GetHouseOwnerList(NewOwner);

  if(List.size() >= MAX_HOUSES_ON_PLAYER)
    SendSysMessage(NewOwner, "Ta osoba posiada juz "+List.size()+" dom"+Gam(List.size(),GAM_OW)+".", FONT_NORMAL, COLOR_RED);
    return;
  endif

  SendSysMessage(NewOwner,"Czy chcesz zostac wlascicielem domu "+GetHouseName(gHouse)+" ?",FONT_NORMAL,COLOR_GREEN);
  if(!YesNo(NewOwner,"Tak ?"))
    SendSysMessage(who,NewOwner.name+" nie chce zostac wlascicielem tego domu.",FONT_NORMAL,COLOR_RED);
    SendSysMessage(NewOwner,"Anulowano.");
    return;
  endif
  
  SetHouseOwner(gHouse,NewOwner);
  SendSysMessage(who,"Nowym wlascicielem domu jest "+NewOwner.name,FONT_NORMAL,COLOR_GREEN);
  SendSysMessage(NewOwner,"Zostal"+ggm(NewOwner,3)+" nowym wlascicielem domu "+GetHouseName(gHouse)+".",FONT_NORMAL,COLOR_GREEN);
  exit;

endfunction

//-----------------------------------------------------------------------------

function DestroyHouseAndGetDeed()

  NoLongerBusy(who);

  if(!IsHOwner and !CanUseCmd(who, "deshouse"))
    SendSysMessage(who,"Nie mozesz tego zrobic.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(GetNotPayedPeriods(gHouse) > 0 and !IsAdmin(who))
    SendSysMessage(who,"Nie mozesz zniszczyc nieoplaconego domu!",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  if(!YesNo(who,"Na pewno?"))
    return;
  endif
  
  if(who.cmdlevel and !GetObjProperty(gHouse,PROP_CREATED)) // aby deed nie mial flagi stworzyciela, jesli domek nie mial takowej flagi.
    ClearScriptObjCreator();
  endif

  var Deed := CreateItemInPack(who,CInt(GetDeedObjTypeByHouseType(gHouse.objtype)));
  if(!Deed)
    SendSysMessage(who,"Nie masz miejsca w plecaku!",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(who.cmdlevel)
    if(CInt(GetObjProperty(gHouse,PROP_CREATED)))
      SetPropCreated(who,Deed);
    endif

    if(!gOwner.cmdlevel)
      LogNotice("GM "+GetRealName(who)+" / "+who.acctname+" zniszczyl dom gracza "+GetRealName(gOwner)+"/"+gOwner.acctname+".");
    endif
  endif
  
  if(!DestroyHouse(gHouse))
    DestroyItem(Deed);
    SendSysMessage(who,"Nie udalo Ci sie zniszczyc domu.",FONT_NORMAL,COLOR_RED);
    return;
  endif
    
  SendSysMessage(who,"Zniszczyl"+ggm(who,3)+" dom i wlozyl"+ggm(who,3)+" deed do plecaka.",FONT_NORMAL,COLOR_GREEN);
  exit;

endfunction

//-----------------------------------------------------------------------------

function SetPrivate()

  if(CInt(GetObjProperty(gHouse, "PublicDelay")) > ReadGameClock())
    SendSysMessage(who, "Musisz odczekac "+CInt(PUBLIC_HOUSE_DELAY/3600)+" godzin by moc zadeklarowac ten dom prywatnym.", FONT_NORMAL, COLOR_RED);
    return;
  endif

  var Vendors := GetHouseVendors(gHouse);
  if(len(Vendors))
    SendSysMessage(who,"Nie mozesz zadeklarowac domu prywatnym gdy kupcy sprzedaja.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  if(SetPrivateHouse(gHouse))
    SendSysMessage(who,"Zadeklarowal"+ggm(who,3)+" ten dom prywatnym, kupcy nie moga juz sprzedawac.",FONT_NORMAL,COLOR_GREEN);
  else
    SendSysMessage(who,"Ten dom jest zadeklarowany prywatnym.",FONT_NORMAL,COLOR_RED);
  endif

endfunction

//-----------------------------------------------------------------------------

function ChangeSignGraphic()

  if(!IsHOwner and !IsCoOwner and !IsAdmin(who))
    SendSysMessage(who,"Nie mozesz tego zrobic.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  StandSignLayout();
  GButtonPage(140,117,2714,2715,1);
  GTextLine(160,117,550,"Znaki Sklepow");

  GButtonPage(320,117,2714,2715,2);
  GTextLine(340,117,550,"Znaki Gildii");

  GPage();
  BuildSignMenu(UOBJ_SHOP_SIGN_START, UOBJ_SHOP_SIGN_END);
  GPage();
  BuildSignMenu(UOBJ_GUILD_SIGN_START, UOBJ_GUILD_SIGN_END);

endfunction

//-----------------------------------------------------------------------------

function BuildSignMenu(SignStart,SignEnd)

  var Graph := 0,w := 0,h := 0;
  for(Graph := SignStart;Graph <= SignEnd;Graph := Graph + 2)
    GButton(122+(w*55),152+(h*40),2714,2715,Graph);
    GTilePic(137+(w*55),152+(h*40),Graph);
    w := w + 1;
    if(w > 5)
      w := 0;
      h := h + 1;
    endif
  endfor

endfunction

//-----------------------------------------------------------------------------

function ChangeGraphic(Graph)

  case(Sign.objtype)
    UOBJ_BRASS_SIGN:   Sign.graphic := Graph;
    UOBJ_WOOD_SIGN:    Sign.graphic := Graph;
    UOBJ_BRASS_SIGN2:  Sign.graphic := Graph - 1;
    UOBJ_WOOD_SIGN2:   Sign.graphic := Graph - 1;
  endcase

endfunction

//-----------------------------------------------------------------------------

function GetSignMsgs()

  if(!GetObjProperty(Sign,"SignMsgs"))
    SetObjProperty(Sign,"SignMsgs",{});
  endif
  
  return GetObjProperty(Sign,"SignMsgs");

endfunction

//-----------------------------------------------------------------------------

function SetSignMsgs(Msgs)

  SetObjProperty(Sign,"SignMsgs",Msgs);

endfunction

//-----------------------------------------------------------------------------

function ShowMsgs()

  GInit();
  GMode(GMODE_MULTIHD, GSMODE_PACKET);
  GSetUnicode();
  GResPic(90,90,2620,380,400);
  GResPic(98,95,3600,367,385);

  GTextLine(130,115,550,"Wiadomosci");

  GButton(125,440,2714,2715,41);
  GTextLine(150,440,550,"Dodaj Wiadomosc");

  var CanCh := 0;
  
  if(IsHOwner or IsCoOwner or IsGm(who))
    if(IsHOwner or IsCoOwner or IsAdmin(who))
      GButton(320,440,2714,2715,42);
      GTextLine(345,440,550,"Wyczysc liste");
    endif

    GTextLine(415,115,550,"Usun");
    CanCh := 1;
  endif
  
  GButton(280,115,2714,2715,9998);
  GTextLine(305,115,550,"Wroc");
  GAddPageData(445,97,2650,2651,445,462,2648,2647);
  GMultiPage();
  
  var Msgs := GetSignMsgs(),Char,i := 1;
  
  foreach Msg in Msgs
    Char := FindPlayerBySerial(Msg[1]);
    GCheckNextPage(7);
    GTextLine(140,145+(GNPCount()*40),570,i);
    GTextLine(165,145+(GNPCount()*40),580,Msg[3]);
    GTextLine(180,165+(GNPCount()*40),560,ClStr(GetRealName(Char)));
    
    GButton(120,145+(GNPCount()*40),2714,2715,30000+i);
    
    if(CanCh)
      GButton(420,145+(GNPCount()*40),2714,2715,300+i);
    endif
    i := i + 1;
  endforeach
  
endfunction

//-----------------------------------------------------------------------------

function CanMsg()

  var Msgs := GetSignMsgs();
  var Pass := 0;
    
  foreach Msg in Msgs
    if(Msg[1] == who.serial and Msg[2] + MSG_TIME > ReadGameClock())
      Pass := Pass + 1;
    endif
  endforeach

  if(Pass >= MSG_NUM)
    SendSysMessage(who,"Mozesz napisac "+MSG_NUM+" wiadomosci w ciagu godziny.",FONT_NORMAL,COLOR_GREEN);
    return 0;
  endif
  
  return 1;

endfunction

//-----------------------------------------------------------------------------

function BuildAddMsg()

  var Res, i, Line;

  while(IsLoggedIn(who) and sign)
    StandSignLayout();
    GSetUnicode();
    GNoClose();
    GTextLine(230,115,550,"Nowa wiadomosc");
    GButton(380,115,2714,2715,9998);
    GTextLine(405,115,550,"Wroc");
    GButton(130,115,2714,2715,43);
    GTextLine(150,115,550,"Dodaj");
  
    GResPic(120,140,BKGND_GOLDTRIM,320,200);
  
    for(i:=0;i<9;i:=i+1)
      if(Lines[i+1])
        Line := Lines[i+1];
      else
        Line := "";
      endif
      GTextEntry(128, 150+(i*20), 300, 20, 560, 10+i, Line);
    endfor

    Res := GSend(who);

    if(Res.butid == 43)
      if(AddNewMsg(Res))
        return;
      endif
    else
      return;
    endif

  endwhile
  
endfunction

//-----------------------------------------------------------------------------

function EraseMsgs()

  if(!YesNo(who,"Skasowac?"))
    SendSysMessage(who,"Anulowano.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  SetSignMsgs({});
  SendSysMessage(who,"Skasowano wiadomosci.",FONT_NORMAL,COLOR_GREEN);

endfunction

//-----------------------------------------------------------------------------

function AddNewMsg(byref res)

  var i, Line;
  Lines := array;

  for(i:=0;i<9;i:=i+1)
    Line := res.unitext[10+i];
    if(!Line)
      if(Lines.size())
        Lines.append(Line);
      endif
    else
      Lines.append(Line);
    endif
  endfor

  if(!Lines.size())
    SendSysMessage(who,"Twoja wiadomosc jest pusta.",FONT_NORMAL,COLOR_RED);
    return 0;
  endif

  if(!CenCheckLines(who, Lines))
    return 0;
  endif
  
  var Msgs   := GetSignMsgs();
  var NewMsg := {};

  NewMsg[1]  := who.serial;
  NewMsg[2]  := ReadGameClock();
  NewMsg[3]  := GetFullDate();
  NewMsg[4]  := who.acctname;
  NewMsg[5]  := Lines;
  Msgs.insert(1, NewMsg);
  SetSignMsgs(Msgs);

  Lines := array;
  SendSysMessage(who,"Dodano wiadomosc.",FONT_NORMAL,COLOR_GREEN);
  return 1;

endfunction

//-----------------------------------------------------------------------------

function EraseMsg(Nr)

  var Msgs := GetSignMsgs();
  Msgs.erase(Nr);
  SetSignMsgs(Msgs);
  SendSysMessage(who,"Skasowano wiadomosc nr "+Nr+".",FONT_NORMAL,COLOR_GREEN);
  
endfunction

//-----------------------------------------------------------------------------

function ShowSignMsg(Nr)

  var Msgs := GetSignMsgs();
  var Msg  := Msgs[Nr],Char;
  
  if(Msg)
    StandSignLayout(60);
    GSetUnicode();
    GTextLine(230,115,550,"Wiadomosc "+Nr+".");
    GButton(380,115,2714,2715,40);
    GTextLine(405,115,550,"Wroc");
    
    if(IsHOwner or IsCoOwner or IsAdmin(who))
      GButton(130,115,2714,2715,300+Nr);
      GTextLine(150,115,550,"Skasuj");
    endif

    Char := FindPlayerBySerial(Msg[1]);
    GTextLine(125,140,590,"Data:");
    GTextLine(170,140,580,Msg[3]);
    GTextLine(125,160,590,"Autor:");
    GTextLine(170,160,560,ClStr(GetRealName(Char)));

    GResPic(120,200,BKGND_GOLDTRIM,320,200);

    var i;
    var MsgText := Msg[5];
    
    for(i:=1;i<=9;i:=i+1)
      GTextLine(128,190+(i*20),560,MsgText[i]);
    endfor
    return 1;
  endif
  
  return 0;

endfunction

//-----------------------------------------------------------------------------

function IfHouseContainsBoundItem( who, house )

  var Items   := house.items;
  var Components := GetAllHouseComponents(house);
  var Dims := GetMultiDimensions(house.graphic);
  foreach Item in Items
    if(!(Item in Components))
      if (GetItemBound(Item))
        SendSysMessage(who,"Nie mozesz przekazac domku, z powodu zablokowanych przedmiotow:",FONT_NORMAL,COLOR_RED);
        SendSysMessage(who,""+ Item.desc + ".",FONT_NORMAL,COLOR_RED);
        
        return 0;
      endif
    endif
  endforeach
  // dodatkowe sprawdzenie
  foreach Item in ListObjectsInBox(house.x + Dims.xmin - 20, house.y + Dims.ymin - 20, -127,house.x + Dims.xmax + 20, house.y + Dims.ymax + 20, 127,house.realm)
    if(Item.isa(POLCLASS_ITEM) and !(Item in Components))
      if(GetHouseSerial(Item) == House.serial)
        if (GetItemBound(Item))
          SendSysMessage(who,"Nie mozesz przekazac domku, z powodu zablokowanych przedmiotow:",FONT_NORMAL,COLOR_RED);
          SendSysMessage(who,""+ Item.desc + ".",FONT_NORMAL,COLOR_RED);
          return 0;
        endif
      endif
    endif
  endforeach

  return 1;

endfunction

//-----------------------------------------------------------------------------

function AcceptTerrain(who)

  if(IsAdmin(who))
    if(GetObjProperty(gHouse, "TerrainAccepted"))
      SendSysMessage(who, "Wylaczyles akceptacje terenu dla tego domu.", FONT_NORMAL, COLOR_GREEN);
      EraseObjProperty(gHouse, "TerrainAccepted");
    else
      SendSysMessage(who, "Wlaczyles akceptacje terenu dla tego domu.", FONT_NORMAL, COLOR_BLUE);
      SetObjProperty(gHouse, "TerrainAccepted", 1);
    endif
  endif

endfunction

//-----------------------------------------------------------------------------

function AcceptTerrainTemporary(who)

  if(IsAdmin(who))
    if(GetObjProperty(gHouse, "TerrainAccepted"))
      SendSysMessage(who, "Wylaczyles akceptacje terenu dla tego domu.", FONT_NORMAL, COLOR_GREEN);
      EraseObjProperty(gHouse, "TerrainAccepted");
    else
      SendSysMessage(who, "Wlaczyles TYMCZASOWA akceptacje terenu dla tego domu.", FONT_NORMAL, COLOR_WHITE);
      SetObjProperty(gHouse, "TerrainAccepted", ReadGameClock() + 259200);
    endif
  endif

endfunction

//-----------------------------------------------------------------------------

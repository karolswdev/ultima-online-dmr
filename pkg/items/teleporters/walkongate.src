//--------------------------------------------------------------------------------

include "include/move";
include "include/checks/locchecks";
include "include/portals";
include "include/mobile/misc";
include "include/mobile/age";
include "include/player/misc";
include "include/client/effects";
include ":instances:instances";

//--------------------------------------------------------------------------------

include ":teleporters:common";

//--------------------------------------------------------------------------------

program WalkOnGate(who, gate)

  if(GetObjProperty(gate, "LinkedInstanceStone"))
	var stone := SystemFindObjectBySerial(CInt(GetObjProperty(gate, "LinkedInstanceStone")));
	
	if(!who.cmdlevel)
		if(!IsParticipant(stone, who))
			SendSysMessage(who, "Nie jestes uczestnikiem wyprawy do " + GetInstanceName(stone));
			return;
		endif
	endif
	
   endif

  if(GetObjProperty(Gate,"NoTele"))
    return;
  elseif(!CanPortalTele(who))
    return; 
  endif

  if(GetGraphicForItem(gate.objtype) != gate.graphic and !GetTeleNr(gate))
    return;
  endif

  if(GetObjProperty(gate, "NoHostiles"))
    if(IsEngaged(who))
      SendSysMessage(who, "Musisz ochlonac po walce!", FONT_NORMAL, COLOR_BAD);
      return;
    endif
  endif

  if(CInt(GetObjProperty(gate,"DestGateSer")))
    if(!FindItemBySerial(GetObjProperty(gate,"DestGateSer")))
      return;
    endif
  endif

  if(who.isa(POLCLASS_NPC))
    return;
  endif
  
  if(!who.cmdlevel)
    if(GetObjProperty(gate,"FireMoon") == 2)
      if(who.dead)
        SendSysMessage(who, "Duchy nie moga wejsc do tej komnaty.", FONT_NORMAL, COLOR_RED);
        return;
      endif
      if(GetRealAge(who) < 25)
        SendSysMessage(who, "Jestes zbyt mlody aby wejsc do tej komnaty.", FONT_NORMAL, COLOR_RED);
        return;
      endif
      if(who.hidden)
        who.hidden := 0;
      endif
    endif
  endif
  
  var x   := GetObjProperty( gate, "DestX" );
  var y   := GetObjProperty( gate, "DestY" );
  var z   := GetObjProperty( gate, "DestZ" );
  var Rlm := GetObjProperty( gate, "DestRlm" );

  var cX   := who.x;
  var cY   := who.y;
  var cRlm := who.realm;
  var Res := 0;

  if(Gate.objtype == UOBJ_TELE)
    foreach Mob in ListHostiles(who, 15)
      if(Mob.isa(POLCLASS_NPC))
        if(!IsTamed(Mob))
          Detach();
          if(IsDungeon(who.x, who.y, who.realm))
            SendSysMessage(who, "Probujesz wyjsc z lochu...", FONT_NORMAL, COLOR_GREEN);
          else
            SendSysMessage(who, "Probujesz uciec...", FONT_NORMAL, COLOR_GREEN);
          endif

          var i;
          var tMax := 15+RandomInt(10);

          for(i:=1;i<=tMax;i:=i+1)
            if(coordist(cX, cY, who.x, who.y) > 2 or cRlm != who.realm)
              SendSysMessage(who, "Oddalil"+ggm(who,3)+" sie.", FONT_NORMAL, COLOR_RED);
              return;
            endif
            sleepms(500);
            who.hidden := 0;
          endfor

          if(IsDungeon(cX, cY, cRlm))
            SendSysMessage(who,"Udalo Ci sie uciec z lochu!", FONT_NORMAL, COLOR_GREEN);
          else
            SendSysMessage(who,"Udalo Ci sie uciec.", FONT_NORMAL, COLOR_GREEN);
          endif
          break;
        endif
      endif
    endforeach
    Res := PortalTele(who, x, y, z, Rlm, MOVETYPE_TELE);
  else
    if(IsPortalDelayed(Gate, who))
      SendSysMessage(who, "Probujesz przejsc przez portal...", FONT_NORMAL, COLOR_GREEN);
      var GameClock := ReadGameClock() + 3 + RandomInt(3);
      while(GameClock > ReadGameClock())
        if(cX != who.x or cY != who.y or cRlm != who.realm)
          SendSysMessage(who, "Oddalil"+ggm(who,3)+" sie.", FONT_NORMAL, COLOR_RED);
          return;
        elseif(!Gate)
          SendSysMessage(who, "Portal sie zamknal.", FONT_NORMAL, COLOR_RED);
          return;
        endif
        Sleepms(100);
        who.hidden := 0;
      endwhile
    endif

    Res := PortalTele(who, x, y, z, Rlm, MOVETYPE_PORTAL);
    
    var Creator := FindPlayerBySerial(GetObjProperty(Gate, "Creator"));

    if(Creator and Creator.murderer != who.murderer and !IsStaff(who))
      DealDamage(who, GetHp(who)/3, who);
      PlayObjectCenteredEffect(who,FX_CURSE_EFFECT,0xF,0xF);
      SendBad(who, "Przejscie przez ten portal dziwnie oslabilo Cie!");
    endif
  endif

  if(Res)
    var CarryProp := GetObjProperty(Gate, "CarryProp");
    if(CarryProp and CarryProp.size() == 3 and !IsStaff(who))
      if(CarryProp[3] == "set")
        SetObjProperty(who, CarryProp[1], CarryProp[2]);
      else
        EraseObjProperty(who, CarryProp[1]);
      endif
    endif
  endif

endprogram

//--------------------------------------------------------------------------------

function IsPortalDelayed(Gate, who)

  if(IsStaff(who))
    return 0;
  endif

  var DruPortal := CInt(GetObjProperty(Gate, "DruPortal"));

  if(DruPortal)
    var Druid := FindPlayerBySerial(DruPortal);
    if(Druid)
      if(!IsFriend(who, Druid) and !who.cmdlevel and who != Druid)
        return 1;
      endif
    endif
  endif

  var Creator := FindPlayerBySerial(GetObjProperty(Gate, "Creator"));

  if(Creator and Creator.murderer != who.murderer)
   /* if(Creator.murderer)
      SetCriminal(who, 1, "WalkOnGate", "Wszedl do portalu mordercy.");
    endif*/
    return 1;
  endif

  return 0;

endfunction

//--------------------------------------------------------------------------------
